# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>

pkgname=copyq
_realname=CopyQ
pkgver=2.4.7
pkgrel=1
pkgdesc="Clipboard manager with searchable and editable history."
url="https://github.com/hluk/CopyQ"
groups=('tools')
depends=('qt5-base' 'libxtst' 'qt5-webkit' 'qt5-svg' 'qt5-tools' 'qt5-script')
optdepends=('copyq-plugin-itemweb')
makedepends=('cmake')
license=('GPL3')
arch=('i686' 'x86_64')
source=("https://github.com/hluk/${_realname}/archive/v${pkgver}.tar.gz")
md5sums=('4215c5e3fc0c5bb050b79f07cfb88a20')
install=copyq.install
_allowed_in=('main' 'staging')
build() {
    mkdir "${srcdir}/build"
    cd "${srcdir}/build"
    cmake -DWITH_WEBKIT=1 \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DQT_QMAKE_EXECUTABLE=qmake-qt5 \
        -DWITH_QT5=TRUE \
        "${srcdir}/${_realname}-${pkgver}"
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}" install
}

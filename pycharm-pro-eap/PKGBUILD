# Maintainer: Dustin Falgout <dustin@falgout.us>
# Contributor: Gifts <gifts.antichat@gmail.com>
# Contributor: Andrey Vlasovskikh <andrey.vlasovskikh@gmail.com>


pkgname=pycharm-pro-eap
_buildver=139.1001
_pkgver=4.0.4
_eap="False"
pkgver=4.0.4.139.1001
pkgrel=1
pkgdesc="Powerful Python and Django IDE, Early Access Program (EAP) build. Professional edition."
arch=('any')
options=('!strip')
url="http://www.jetbrains.com/pycharm/"
license=("custom")
depends=("java-environment>=6")
provides=("pycharm-eap" "pycharm-professional")
conflicts=("pycharm-eap" "pycharm-community" "pycharm-professional")
changelog=""
if [[ ${_eap} = "True" ]]; then
	source=("http://download.jetbrains.com/python/pycharm-professional-${_buildver}.tar.gz")
else
	source=("http://download.jetbrains.com/python/pycharm-professional-${_pkgver}.tar.gz")
fi
md5sums=('b59bf09ed42349452977e12ec0b3eebd')

package() {
  cd "${srcdir}"
  mkdir -p "${pkgdir}/opt/${pkgname}"
  if [[ ${_eap} = "True" ]]; then
  	cp -R "${srcdir}/pycharm-${_buildver}/"* "${pkgdir}/opt/${pkgname}"
  else
  	cp -R "${srcdir}/pycharm-${_pkgver}/"* "${pkgdir}/opt/${pkgname}"
  fi
  if [[ $CARCH = 'i686' ]]; then
     rm -f "${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux64.so"
     rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier64"
  fi
(
cat <<EOF
[Desktop Entry]
Version=$pkgver
Type=Application
Name=PyCharm-EAP
Exec="/opt/$pkgname/bin/pycharm.sh" %f
Icon=/opt/$pkgname/bin/pycharm.png
Comment=$pkgdesc
Categories=Development;IDE;
Terminal=false
StartupNotify=true
StartupWMClass=jetbrains-pycharm
EOF
) > "${srcdir}/${pkgname}.desktop"

  mkdir -p "${pkgdir}/usr/bin/"
  mkdir -p "${pkgdir}/usr/share/applications/"
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/"
   if [[ ${_eap} = "True" ]]; then
  	cp -R ${srcdir}/pycharm-${_buildver}/license/* "${pkgdir}/usr/share/licenses/${pkgname}/"
  else
  	cp -R ${srcdir}/pycharm-${_pkgver}/license/* "${pkgdir}/usr/share/licenses/${pkgname}/"
  fi
  ln -s "/opt/${pkgname}/bin/pycharm.sh" "${pkgdir}/usr/bin/pycharm-eap"
}


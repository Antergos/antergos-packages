# Maintainer: Dustin Falgout <dustin@falgout.us>
# Contributor: Gifts <gifts.antichat@gmail.com>
# Contributor: Andrey Vlasovskikh <andrey.vlasovskikh@gmail.com>

pkgname=pycharm-pro-eap
_buildver=139.431
_pkgver=4.0.0
pkgver=4.0.0.139.431
pkgrel=1
pkgdesc="Powerful Python and Django IDE, Early Access Program (EAP) build. Professional edition."
arch=('any')
options=('!strip')
url="http://www.jetbrains.com/pycharm/"
license=("custom")
depends=("java-environment>=6")
provides=("pycharm-eap" "pycharm-professional")
conflicts=("pycharm-eap" "pycharm-community" "pycharm-professional")
changelog=""
source=("http://download.jetbrains.com/python/pycharm-professional-${_buildver}.tar.gz")
md5sums=('25b638c5e7ed9025d866d6ce694255d2')

package() {
  cd "${srcdir}"
  mkdir -p "${pkgdir}/opt/${pkgname}"
  cp -R "${srcdir}/pycharm-${_buildver}/"* "${pkgdir}/opt/${pkgname}"
  if [[ $CARCH = 'i686' ]]; then
     rm -f "${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux64.so"
     rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier64"
  fi
(
cat <<EOF
[Desktop Entry]
Version=$pkgver
Type=Application
Name=PyCharm-EAP
Exec="/opt/$pkgname/bin/pycharm.sh" %f
Icon=/opt/$pkgname/bin/pycharm.png
Comment=$pkgdesc
Categories=Development;IDE;
Terminal=false
StartupNotify=true
StartupWMClass=jetbrains-pycharm
EOF
) > "${srcdir}/${pkgname}.desktop"

  mkdir -p "${pkgdir}/usr/bin/"
  mkdir -p "${pkgdir}/usr/share/applications/"
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/"
  cp -R ${srcdir}/pycharm-${_buildver}/license/* "${pkgdir}/usr/share/licenses/${pkgname}/"
  ln -s "/opt/${pkgname}/bin/pycharm.sh" "${pkgdir}/usr/bin/pycharm-eap"
}


# $Id: PKGBUILD 126461 2015-01-23 20:22:27Z faidoc $
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ner0

pkgname=nemo
pkgver=3.0.5
pkgrel=2
pkgdesc="Cinnamon file manager (Nautilus fork)"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
license=('GPL')
groups=('cinnamon')
depends=('libexif' 'gvfs' 'dconf' 'desktop-file-utils' 'exempi' 'python2'
		'cinnamon-desktop' 'libnotify' 'libxml2' 'cinnamon-translations')
makedepends=('gtk-doc' 'gobject-introspection' 'intltool' 'gnome-common'
			'python-gobject' 'python-polib' 'python2-gobject' 'python2-polib')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/nemo/tarball/$pkgver")
sha256sums=('67a4607e0f311db3b71c2014ab2be4c0d4c14f272fc019d381c77acf8442ad77')


# BEGIN ANTBS METADATA

_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='linuxmint'
_monitored_repo='nemo'

# END ANTBS METADATA


prepare() {
	cd linuxmint-nemo-*

	# Python2 fix
	find -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

	# Rename 'Files' app name to avoid having the same as nautilus
	sed -i 's/^Name\(.*\)=.*/Name\1=Nemo/' data/nemo.desktop.in.in

}


build() {
	cd linuxmint-nemo-*

	./autogen.sh --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var --disable-static \
		--libexecdir=/usr/lib/nemo \
		--disable-update-mimedb \
		--disable-tracker \
		--disable-gtk-doc-html \
		--disable-schemas-compile
	make
}

package() {
	cd linuxmint-nemo-*

	make DESTDIR="${pkgdir}" install
}

# -*- mode: bash;-*-
 

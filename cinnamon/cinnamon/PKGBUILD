# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: M0Rf30
# Contributor: unifiedlinux
# Contributor: CReimer

pkgname=cinnamon
pkgver=3.0.5
pkgrel=2
pkgdesc='Linux Desktop featuring a traditional layout built using modern technologies.'
arch=('i686' 'x86_64')
url='http://cinnamon.linuxmint.com/'
license=('GPL2')
groups=('cinnamon')
depends=('accountsservice'          'cjs'                   'librsvg'                'python2-lxml'
		 'caribou'                  'clutter-gtk'           'muffin'                 'python2-pam'
		 'cinnamon-control-center'  'gnome-backgrounds'     'nemo'                   'python2-pexpect'
		 'cinnamon-menus'           'gnome-themes-standard' 'networkmanager'         'python2-pillow'
		 'cinnamon-screensaver'     'gconf'                 'network-manager-applet' 'python2-ptyprocess'
		 'cinnamon-session'         'gstreamer'             'polkit-gnome'           'python2-pyinotify'
		 'cinnamon-settings-daemon' 'libgnome-keyring'      'python2-cairo'          'webkitgtk'
		 'cinnamon-translations'    'libgnomekbd'           'python2-dbus')
makedepends=('gnome-common' 'intltool' 'gtk-doc' 'gobject-introspection')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/Cinnamon/archive/$pkgver.tar.gz"
		"set_wheel.patch"
		"default-theme.patch"
		"fix-missing-icons.patch")
sha256sums=('2d37f935d0082c33eaaf65c523a03a641b7ec29273b6b6aa541fe313570f23ed'
			'9f1d4f6b3391f6ff0a6761fa4bcfab297a5e1185a3d8ff24a760284b667366e2'
			'566585873f38a79ec248b916645a2e081abec3c6d4df2c34339cde1f35375cc5'
			'a73f704bc6a0fcd6a003f28be23f035eb506ace484ebb82a25826634226338b0')


# BEGIN ANTBS METADATA

_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='linuxmint'
_monitored_repo='cinnamon'

# END ANTBS METADATA


prepare() {
	cd "${srcdir}"/Cinnamon*

	# Use wheel group instread of sudo
	patch -Np1 -i ../set_wheel.patch

	# Set default theme to 'cinnamon'
	patch -Np1 -i ../default-theme.patch

	# Fix missing icons with Adwaita theme
	patch -Np1 -i ../fix-missing-icons.patch

	# Python2 fix
	find -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

	# Add polkit agent to required components
	sed -i 's/RequiredComponents=\(.*\)$/RequiredComponents=\1polkit-gnome-authentication-agent-1;/' \
		files/usr/share/cinnamon-session/sessions/cinnamon*.session

	# Use pkexec instead of gksu
	sed -i 's/gksu/pkexec/' files/usr/bin/cinnamon-settings-users

	# Fix selected background color in Cinnamon Settings for Adwaita theme
	sed -i 's/@selected_bg_color;/@theme_selected_bg_color;/' \
	files/usr/share/cinnamon/cinnamon-settings/cinnamon-settings.py
}

build() {
	cd "${srcdir}"/Cinnamon*

	./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/cinnamon \
		--localstatedir=/var \
		--disable-static \
		--disable-schemas-compile \
		--enable-compile-warnings=yes

	make
}

package() {
	cd "${srcdir}"/Cinnamon*
	make DESTDIR="${pkgdir}" install
}

# -*- mode: bash;-*-

#
# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>
#
pkgname="zfs-utils"
pkgver=0.6.5.5
_pkgver=0.6.5.5
pkgrel=1
license=('CDDL')
pkgdesc="Kernel module support files for the Zettabyte File System."
depends=("spl" "dkms" "linux-headers")
makedepends=("git")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=("zfs::https://github.com/zfsonlinux/zfs/releases/download/zfs-${pkgver}/zfs-${pkgver}.tar.gz"
        "zfs-utils.bash-completion-r1"
        "zfs-utils.initcpio.install"
        "zfs-utils.initcpio.hook")
groups=("system")
md5sums=('SKIP'
         '9ddb0c8a94861f929d0fa741fdc49950'
         '110b48d872a7a7d2884496267f92418a'
         '62ba32c6853c315ff231c396cf8bfdbb')
conflicts=("zfs-utils-lts")

_kernel=4.4.5
_kernel=4.4.5

#pkgver() {
#    cd "${srcdir}/zfs"
#    printf "%s.r%s_%s" "${_pkgver}" "$(git rev-list --count HEAD)" "${_kernel}"
#}

build() {
    cd "${srcdir}/zfs-${pkgver}"
    ./autogen.sh
    
    _i686_target=""
    [[ "${CARCH}" == i686 ]] && _i686_target="--target=i686-linux-gnu"

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --sbindir=/usr/bin \
                --with-mounthelperdir=/usr/bin \
                --libdir=/usr/lib \
                --datadir=/usr/share \
                --includedir=/usr/include \
                --with-udevdir=/usr/lib/udev \
                --libexecdir=/usr/lib/zfs \
                --with-config=user \
                ${_i686_target}
    make
}

package() {

    cd "${srcdir}/zfs-${pkgver}"
    make DESTDIR="${pkgdir}" install

    # Remove uneeded files
    rm -r "${pkgdir}"/etc/init.d
    rm -r "${pkgdir}"/usr/lib/dracut

    install -D -m644 "${srcdir}"/zfs-utils.initcpio.hook "${pkgdir}"/usr/lib/initcpio/hooks/zfs
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.install "${pkgdir}"/usr/lib/initcpio/install/zfs
    install -D -m644 "${srcdir}"/zfs-utils.bash-completion-r1 "${pkgdir}"/usr/share/bash-completion/completions/zfs
}
# -*- mode: bash;-*-

# Maintainer: Antergos Developers <dev@antergos.com>
# Contributors: Achilleas Pipinellis, speed145a, Schnouki, Det

pkgname=firefox-beta
pkgver=48.0b9
_major=${pkgver/rc*}
_build=${pkgver/*rc}
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org - Beta"
arch=('i686' 'x86_64')
url="https://www.mozilla.org/en-US/firefox/channel/#beta"
license=('MPL' 'GPL' 'LGPL')
groups=('browser')
depends=('alsa-lib' 'dbus-glib' 'desktop-file-utils' 'gtk2' 'gtk3' 'hicolor-icon-theme'
		'icu' 'libevent' 'libvpx' 'libxt' 'mime-types' 'nss' 'sqlite')
optdepends=('gst-plugins-base: vorbis decoding, ogg demuxing'
			'gst-plugins-good: webm and mp4 demuxing'
			'gst-plugins-bad: aac, vp8 and opus decoding'
			'gst-plugins-ugly: h.264 and mp3 decoding'
			'gst-libav: more decoders'
			'libpulse: PulseAudio driver'
			'networkmanager: Location detection via available WiFi networks')
provides=("firefox=${pkgver}" 'firefox-beta' 'firefox-beta-bin')
conflicts=('firefox-beta-bin' 'firefox-beta')
source=("$pkgname.desktop")
source_i686=("firefox-${pkgver}-i686.tar.bz2::https://ftp.mozilla.org/pub/firefox/releases/$pkgver/linux-i686/en-US/firefox-$pkgver.tar.bz2")
source_x86_64=("firefox-${pkgver}-x86_64.tar.bz2::https://ftp.mozilla.org/pub/firefox/releases/$pkgver/linux-x86_64/en-US/firefox-$pkgver.tar.bz2")

if [[ "${_build}" != "${pkgver}" ]]; then
	# RC
	source_i686=("firefox-${_major}-i686.tar.bz2::https://ftp.mozilla.org/pub/firefox/candidates/$_major-candidates/build$_build/linux-i686/en-US/firefox-$_major.tar.bz2")
	source_x86_64=("firefox-${_major}-x86_64.tar.bz2::https://ftp.mozilla.org/pub/firefox/candidates/$_major-candidates/build$_build/linux-x86_64/en-US/firefox-$_major.tar.bz2")
fi

md5sums=('259ea8127d1f3a72ac23db0a97455472')
md5sums_i686=('55a131dead6f3943769c1f1f969ded8a')
md5sums_x86_64=('a037d792be43db94acb21542e042c426')

package() {
  # Create directories
  msg2 "Creating directory structure..."
  mkdir -p "${pkgdir}/usr/bin"
  mkdir -p "${pkgdir}/usr/share/applications"
  mkdir -p "${pkgdir}/usr/share/icons/hicolor/128x128/apps"
  mkdir -p "${pkgdir}/opt"

  msg2 "Moving stuff in place..."
  # Install
  cp -r firefox/ "${pkgdir}/opt/${pkgname}"

  # /usr/bin link
  ln -s /opt/$pkgname/firefox "${pkgdir}/usr/bin/${pkgname}"
  ln -s /opt/$pkgname/firefox "${pkgdir}/usr/bin/${pkgname}-bin"

  # Desktops
  install -m644 *.desktop "$pkgdir"/usr/share/applications/

  # Icons
  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
    install -d "$pkgdir"/usr/share/icons/hicolor/$i/apps/
    ln -s /opt/$pkgname/browser/chrome/icons/default/default${i/x*}.png \
          "$pkgdir"/usr/share/icons/hicolor/$i/apps/$pkgname.png
  done

  ln -s /opt/$pkgname/browser/icons/mozicon128.png \
        "$pkgdir"/usr/share/icons/hicolor/128x128/apps/$pkgname.png
}
# -*- mode: bash;-*-

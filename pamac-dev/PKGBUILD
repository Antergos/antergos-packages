# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Guillaume Benoit <guillaume@manjaro.org>
pkgname=pamac-dev
pkgver=2.3.1
_pkgver=2.3.1
pkgrel=1
# This is the release package so the below _gitcommit variable should (usually) be commented out.
_gitcommit="master"
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/pamac"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'yaourt' 'vte3>=0.38' 'gtk3>=3.16' 'libnotify' 'desktop-file-utils')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome' 'lxsession: needed for authentification in Xfce, LXDE etc.')
makedepends=('gettext' 'itstool' 'vala>=0.28' 'git')
backup=('etc/pamac.conf')
provides=('pamac')
conflicts=('pamac' 'pamac-aur')
options=(!emptydirs)
install=pamac.install

if [ "${_gitcommit}" != "" ]; then
  source=("git+https://github.com/manjaro/pamac")
else
  source=("pamac-$pkgver-$pkgrel.tar.gz::$url/archive/v$_pkgver.tar.gz")
fi

sha256sums=('SKIP')

pkgver() {
  if [[ "${srcdir}" != '' ]]; then
	  cd "$srcdir/pamac"
	fi
	printf "%s.r%s" "$_pkgver" "$(git rev-list --count HEAD)"

}

prepare() {
  if [ "$_gitcommit" != "" ]; then
    cd "$srcdir/pamac"
  else
    cd "$srcdir/pamac-$_pkgver"
  fi

  # adjust version string
  cd src
  sed -i -e "s|\"2.1\"|\"$pkgver-$pkgrel\"|g" manager_window.vala
}

build() {
  if [ "$_gitcommit" != "" ]; then
    cd "$srcdir/pamac"
  else
    cd "$srcdir/pamac-$_pkgver"
  fi

  # build
  make all
}
package() {
  if [ "$_gitcommit" != "" ]; then
    cd "$srcdir/pamac"
  else
    cd "$srcdir/pamac-$_pkgver"
  fi

  mkdir -p "${pkgdir}/usr/share/icons/hicolor"
  cp -r "data/icons/"* "${pkgdir}/usr/share/icons/hicolor"
  cp -r "po/locale" "${pkgdir}/usr/share"
  install -Dm744 "src/pamac-daemon" "${pkgdir}/usr/bin/pamac-daemon"
  install -Dm755 "src/pamac-manager" "${pkgdir}/usr/bin/pamac-manager"
  install -Dm755 "src/pamac-updater" "${pkgdir}/usr/bin/pamac-updater"
  install -Dm755 "src/pamac-tray" "${pkgdir}/usr/bin/pamac-tray"
  install -Dm755 "src/pamac-refresh" "${pkgdir}/usr/bin/pamac-refresh"
  install -Dm755 "src/pamac-install" "${pkgdir}/usr/bin/pamac-install"
  install -Dm644 "data/applications/pamac-tray.desktop" "${pkgdir}/etc/xdg/autostart/pamac-tray.desktop"
  install -Dm644 "data/applications/pamac-manager.desktop" "${pkgdir}/usr/share/applications/pamac-manager.desktop"
  install -Dm644 "data/applications/pamac-updater.desktop" "${pkgdir}/usr/share/applications/pamac-updater.desktop"
  install -Dm644 "data/applications/pamac-install.desktop" "${pkgdir}/usr/share/applications/pamac-install.desktop"
  install -Dm644 "data/config/pamac.conf" "${pkgdir}/etc/pamac.conf"
  install -Dm644 "data/dbus/org.manjaro.pamac.conf" "${pkgdir}/etc/dbus-1/system.d/org.manjaro.pamac.conf"
  install -Dm644 "data/dbus/org.manjaro.pamac.service" "${pkgdir}/usr/share/dbus-1/system-services/org.manjaro.pamac.service"
  install -Dm644 "data/systemd/pamac.service" "${pkgdir}/usr/lib/systemd/system/pamac.service"
  install -Dm744 "data/networkmanager/99_update_pamac_tray" "${pkgdir}/etc/NetworkManager/dispatcher.d/99_update_pamac_tray"
  install -Dm644 "data/polkit/org.manjaro.pamac.policy" "${pkgdir}/usr/share/polkit-1/actions/org.manjaro.pamac.policy"
  install -Dm644 "data/mime/x-alpm-package.xml" "${pkgdir}/usr/share/mime/packages/x-alpm-package.xml"
}

# vim:set ts=2 sw=2 et:

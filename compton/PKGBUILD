# Maintainer: OK100 <ok100 at lavabit dot com>
# Previous Maintainer: Val√®re Monseur <valere dot monseur at ymail dot com>

pkgname=compton
_gitname=compton
pkgver=2014.01.21.gaeda148
pkgrel=1
pkgdesc="X Compositor (a fork of xcompmgr-dana)"
arch=(i686 x86_64)
url="https://github.com/chjj/compton"
license=('MIT')
depends=('libgl' 'libdbus' 'libxcomposite' 'libxdamage' 'libxrandr' 'pcre' 'libconfig' 'libxinerama')
optdepends=('xorg-xprop: for compton-trans'
            'xorg-xwininfo: for compton-trans')
makedepends=('git' 'asciidoc')
source=("git://github.com/chjj/compton.git")
md5sums=("SKIP")

pkgver() {
    cd "${srcdir}/${_gitname}"
    git log -1 --format="%cd.g%h" --date=short | sed 's/-/./g'
}

build() {
  cd "$srcdir/$_gitname"
  make PREFIX=/usr
  make docs
}

package() {
  cd "$srcdir/$_gitname"

  make PREFIX="$pkgdir/usr" install

  # install license
  install -D -m644 "LICENSE" "$pkgdir/usr/share/licenses/$_gitname/LICENSE"

  # example conf
  install -D -m644 "compton.sample.conf" "$pkgdir/etc/xdg/compton.conf.example"
}

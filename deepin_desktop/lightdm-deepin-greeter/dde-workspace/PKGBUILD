# Maintainer: Steve Ovens <steve_ovens [AT] linux DOT com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgbase=dde-workspace
pkgname=dde-workspace
pkgver=2.1.20150521160615
_srcdirname=deepin-desktop-environment-2.1+20150521160615
pkgrel=1
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-workspace"
license=('GPL3')
groups=('deepin')
makedepends=('cmake' 'go' 'coffee-script' 'go-gettext-go' 'webkitgtk'
             'libdeepin-webkit' 'lightdm' 'cairo' 'dbus-glib' 'python2')
source=("http://deepin.mirror.gtcomm.net/pool/main/d/deepin-desktop-environment/deepin-desktop-environment_2.1+20150521160615.tar.gz"
        "default-display-manager")
sha256sums=('20aa7a8df56f4be815a5525aa33685365998e9cc63a4dc94e1cd8bc7db85300f'
            '3667d6cce9a00f0b8c8bc27250b2ceaf421382d3b4e687a5bcea4c64292030df')

build() {
  cd "${_srcdirname}"

  #export PYTHON=/usr/bin/python2
  mkdir -p build && cd build/
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release && make

  gcc -o ../debian/default-terminal ../debian/default-terminal.c `pkg-config --libs --cflags glib-2.0 gio-unix-2.0`

}

package() {
  pkgdesc="Linux Deepin desktop environment."
  depends=('dde-control-center' 'deepin-menu' 'deepin-artwork'
           'libdeepin-webkit' 'cairo' 'dbus-glib' 'python2')
  optdepends=('lightdm-deepin-greeter')
  replaces=("deepin-desktop-environment" "deepin-workspace")
  install="${pkgbase}.install"

  cd "${_srcdirname}"/build

  make install DESTDIR="${pkgdir}"

  install -dm755 "${pkgdir}"/{etc/{X11,sysctl.d},usr/share/backgrounds}

  install -m644 ../debian/30-deepin-inotify-limit.conf "${pkgdir}"/etc/sysctl.d/
  install -m755 ../debian/default-terminal "${pkgdir}"/usr/bin/
  install -m644 "${srcdir}"/default-display-manager "${pkgdir}"/etc/X11/

  ## add default background..
  install -m644 ../resources/greeter/images/background/default_background.jpg "${pkgdir}"/usr/share/backgrounds/
}



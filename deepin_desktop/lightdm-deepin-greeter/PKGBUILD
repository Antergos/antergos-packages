# Maintainer: Steve Ovens <steve_ovens [AT] linux DOT com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgbase=dde-workspace
pkgname=lightdm-deepin-greeter
pkgver=2.1.20150521160615
_srcdirname=deepin-desktop-environment-2.1+20150521160615
pkgrel=1
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-workspace"
license=('GPL3')
groups=('deepin')
makedepends=('cmake' 'go' 'coffee-script' 'go-gettext-go' 'webkitgtk'
             'libdeepin-webkit' 'lightdm' 'cairo' 'dbus-glib' 'python2')
source=("http://deepin.mirror.gtcomm.net/pool/main/d/deepin-desktop-environment/deepin-desktop-environment_2.1+20150521160615.tar.gz"
        "default-display-manager")
sha256sums=('20aa7a8df56f4be815a5525aa33685365998e9cc63a4dc94e1cd8bc7db85300f'
            '3667d6cce9a00f0b8c8bc27250b2ceaf421382d3b4e687a5bcea4c64292030df')

prepare() {
  cp -r "${_srcdirname}" "${_srcdirname}-greeter"

  cd "${srcdir}"/${_srcdirname}
  sed "/add_subdirectory(greeter)/d" -i app/CMakeLists.txt || die "sed failed"
}

build() {
  cd "${srcdir}"/${_srcdirname}-greeter

  mkdir -p build && cd build/
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release && make
}

package() {
  pkgdesc="Lightdm greeter for Linux Deepin desktop environment."
  depends=('cairo' 'gtk3' 'libdeepin-webkit' 'lightdm' 'nodejs' 'python2')

  cd "${srcdir}"/${_srcdirname}-greeter/build/app/greeter

  make install DESTDIR="${pkgdir}"
  chmod 0700 "${pkgdir}"/var/lib/polkit-1
}


# Maintainer: Steve Ovens <steve_ovens [AT] linux DOT com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screenshot
pkgver=3.0.201504271302008
_srcdirname=deepin-screenshot-3.0+20150427130200~34d48c47e58
pkgrel=1
pkgdesc="Easy-to-use screenshot tool for linuxdeepin desktop environment"
arch=('any')
url="https://gitcafe.com/Deepin/deepin-screenshot"
license=('GPL3')
makedepends=('deepin-gettext-tools')
groups=('deepin-extra')
install="${pkgname}.install"
source=("http://deepin.mirror.gtcomm.net/pool/main/d/deepin-screenshot/deepin-screenshot_3.0+20150427130200~34d48c47e58.tar.gz")
sha256sums=('845770eb7697f2025ebc55678f8a82240701171c6af2593bdb055c53c7b7c43e')

# Usage: _easycp dest files...
_easycp () {
  local dest=$1; shift
  mkdir -p "${dest}"
  cp -R -t "${dest}" "$@"
}

prepare() {
  cd "${_srcdirname}"

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd "${_srcdirname}"

  deepin-generate-mo tools/locale_config.ini
}

package() {
  depends=('python2' 'gconf' 'python2-xlib' 'python2-wnck' 'python2-xdg'
           'python2-scipy' 'python2-pycurl' 'python2-deepin-ui')

  cd "${_srcdirname}"

  # TODO waiting for new Makefile
  _easycp "${pkgdir}"/usr/share/deepin-screenshot/ src
  _easycp "${pkgdir}"/usr/share/deepin-screenshot/ theme
  _easycp "${pkgdir}"/usr/share/deepin-screenshot/ skin
  _easycp "${pkgdir}"/usr/share/ locale

  mkdir -p "${pkgdir}"/usr/share/applications/
  install -m 0644 debian/deepin-screenshot.desktop "${pkgdir}"/usr/share/applications/

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /usr/share/deepin-screenshot/src/screenshot.py "${pkgdir}"/usr/bin/deepin-screenshot

  # remove .po
  find "${pkgdir}" -name '*.po' -exec rm '{}' ';'
  find "${pkgdir}" -name '*.pot' -exec rm '{}' ';'
}

 # Local Variables:
 # sh-basic-offset: 2
 # End:

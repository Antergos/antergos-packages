# Maintainer: Steve Ovens <steve_ovens [AT] linux DOT com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dde-control-center
pkgver=0.1.20150413164707
_srcdirname=dde-control-center-0.1+20150413164707~6ec15a41de
pkgrel=3
pkgdesc="New control center for linux deepin"
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-control-center"
license=('GPL3')
depends=('dde-daemon' 'deepin-qml-widgets' 'dde-qml-dbus-factory'
         'qt5-svg' 'qt5-declarative' 'qt5-graphicaleffects' 'qt5-quickcontrols')
makedepends=('deepin-gettext-tools' 'qt5-tools')
groups=('deepin')
options=(staticlibs)
source=("http://deepin.mirror.gtcomm.net/pool/main/d/dde-control-center/dde-control-center_0.1+20150413164707~6ec15a41de.tar.gz")
sha256sums=('729a2c722bb788094e485c92120304078fa14005b36d60d5011b8b49c4cbfcf6')

prepare() {
  cd "${_srcdirname}"

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build(){
  cd "${_srcdirname}"

  make
}

package() {
  depends+=('deepin-artwork-themes' 'dde-account-faces')

  cd "${_srcdirname}"

  make DESTDIR="${pkgdir}" install
}

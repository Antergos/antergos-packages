# Maintainer: Steve Ovens <steve_ovens [AT] linux DOT com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-movie
pkgver=2.2.1.20150424170256
_srcdirname=deepin-movie-2.2.1+20150424170255~9759b316cf
pkgrel=1
pkgdesc='deepin movie for linuxdeepin desktop environment'
arch=('any')
url="https://github.com/linuxdeepin/deepin-movie"
license=('GPL3')
makedepends=('python2-sip' 'deepin-gettext-tools')
groups=('deepin-extra')
install="${pkgname}.install"
source=("http://deepin.mirror.gtcomm.net/pool/main/d/deepin-movie/deepin-movie_2.2.1+20150424170255~9759b316cf.tar.gz")
sha256sums=('e09307f687e46c9aa0b7914d5cbca08fae75c2bfc657cede69de2838223ffd66')

prepare() {
  cd "${_srcdirname}"

  sed 's\python\python2\' -i Makefile

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd "${_srcdirname}"

  make
}

package() {
  depends=('qt5-declarative' 'qt5-quickcontrols' 'qt5-multimedia' 'qt5-webkit'
           'qt5-graphicaleffects' 'qt5-imageformats' 'qt5-systems'
           'python2-ass' 'python2-magic' 'python2-peewee' 'python2-pyqt5'
           'python2-pysrt' 'python2-setproctitle' 'python2-xpybutil'
           'dde-go-dbus-factory' 'deepin-menu' 'deepin-qml-widgets' 'python2-deepin-ui'
           'desktop-file-utils' 'enca' 'mediainfo' 'qtav')

  cd "${_srcdirname}"

  make DESTDIR="${pkgdir}" install
}

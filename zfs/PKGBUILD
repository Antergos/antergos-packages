#
# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>
#


pkgname=zfs
pkgdesc="Kernel modules for the Zettabyte File System."
_pkgver=0.6.5.6
pkgver=${_pkgver}
pkgrel=1
license=('CDDL')
makedepends=("git" "linux-headers" "linux")
depends=("spl" "zfs-utils" "dkms" "linux-headers" "linux")
provides=("zfs-dkms")
conflicts=("zfs-git" "zfs-lts" "zfs-dkms")
install=zfs.install
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=("zfs::git+http://github.com/zfsonlinux/zfs.git")
md5sums=('SKIP')

_kernel='4.4.5-1-ARCH'


pkgver() {
	local _kernel='4.4.5_1'
    cd "${srcdir}/zfs"
    printf "%s.r%s_%s" "${_pkgver}" "$(git rev-list --count HEAD)" "${_kernel}"
}

build() {
	sed -i "s|<PKGVER>|${_pkgver}|g" "${startdir}/${pkgname}.install"
	sed -i "s|<KERNVER>|${_kernel}|g" "${startdir}/${pkgname}.install"
    cd "${srcdir}/zfs"
    ./autogen.sh
    #scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n zfs
    
    _i686_target=""
    [[ "${CARCH}" == i686 ]] && _i686_target="--target=i686-linux-gnu"
    
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --bindir=/usr/bin \
                --sbindir=/usr/bin \
                --libdir=/usr/lib \
                --datadir=/usr/share \
                --includedir=/usr/include \
                --with-udevdir=/usr/lib/udev \
                --libexecdir=/usr/lib/zfs \
                --with-config=user \
                ${_i686_target}
    make
}

package() {
    dkmsdir="${pkgdir}/usr/src/zfs-${_pkgver}"
    install -d "${dkmsdir}"

    cd "${srcdir}/zfs"
    git archive --format=tar HEAD | tar -x -C "${dkmsdir}"

    cd "${dkmsdir}"
    ./autogen.sh
    scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n zfs
    chmod g-w,o-w -R .
}

# -*- mode: bash;-*-

#
# Maintainer: Damian01w <damian01w@gmail.com>
# Maintainer: Padfoot <padfoot@exemail.com.au>
#

pkgname=plymouth
pkgver=0.9.2
pkgrel=1
pkgdesc="A graphical boot splash screen with kernel mode-setting support"
url="http://www.freedesktop.org/wiki/Software/Plymouth/"

arch=('i686' 'x86_64')
license=('GPL')
install=plymouth.install
depends=('libdrm' 'pango' 'systemd' 'gtk3')
makedepends=('docbook-xsl')
optdepends=('ttf-dejavu')

options=('!libtool' '!emptydirs')

provides=('plymouth')
conflicts=('plymouth-git')
backup=('etc/plymouth/plymouthd.conf')

source=("git+http://anongit.freedesktop.org/git/plymouth#tag=0.9.2"
	'antergos.zip'
	'antergos-logo.png'
	'plymouth.encrypt_hook'
	'plymouth.encrypt_install'
	'gdm-plymouth.service'
	'kdm-plymouth.service'
	'sddm-plymouth.service'
	'lxdm-plymouth.service'
	'lightdm-plymouth.service'
	'slim-plymouth.service'
	'plymouth-start.service'
	'plymouth.initcpio_hook'
	'plymouth.initcpio_install'
	'plymouth-quit.service.in.patch'
	'plymouth-set-default-theme.in.patch'
	'plymouth-update-initrd.patch')

md5sums=('SKIP'
         '9050b51ca1062b9c1d5c0918fa172437'
         'd894033cfdb5d3cc31efa4a6121a150e'
         'fbdd5d04e3089a214b9056357434df86'
         '65eb269910f1b81e3f0970af456394ca'
         'c36da17cb580ff901960e73d56a7d88c'
         'dcacff05835053242f52b9e375248ec3'
         'd1804c9f0064fb5862338a0c29d84fa5'
         '22130f7e6b20d2a43dbed924de75180b'
         '870ea3e63c6989e2badf79d1fbafa914'
         'fb2eb948e2198cfb54cc2b710a05ee61'
         '78340207f7391c682601b1b36baed14d'
         '32f04fdbd1eb94ade30d1e63fdcdd9b5'
         '8e4b5075702a69ec25508de0badf366c'
         '165a39dbedcc6e123c8ca05d5b4b2e25'
         'f79edbbb30c71b0dbcd102c7dd31660b'
         '0357775c16b5f90f1af485e6a4c80a9e')

prepare() {
	cd "$srcdir"/${pkgname}
	patch -p1 -i $srcdir/plymouth-set-default-theme.in.patch
	patch -p1 -i $srcdir/plymouth-update-initrd.patch
	patch -p1 -i $srcdir/plymouth-quit.service.in.patch
}

build() {
	cd "$srcdir"/${pkgname}
	
	LDFLAGS="$LDFLAGS -ludev" ./autogen.sh --prefix=/usr \
		--exec-prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--sbindir=/usr/bin \
		--enable-systemd-integration \
		--enable-drm \
		--enable-tracing \
		--enable-pango \
		--enable-gtk=yes \
		--with-release-file=/etc/os-release \
		--with-logo=/usr/share/antergos/antergos-logo.png \
		--with-background-color=0x000000 \
		--with-background-start-color-stop=0x000000 \
		--with-background-end-color-stop=0x4D4D4D \
		--without-rhgb-compat-link \
		--without-system-root-install

	make
}

package() {
  cd "$srcdir"/${pkgname}

  make DESTDIR="$pkgdir" install
  install -Dm644 ../antergos-logo.png             "$pkgdir/usr/share/plymouth/antergos-logo.png"
  cp -R --no-preserve=all ../antergos "$pkgdir/usr/share/plymouth/themes/"
  cd "$srcdir"
  install -Dm644 "plymouth.encrypt_hook"     "$pkgdir/usr/lib/initcpio/hooks/plymouth-encrypt"
  install -Dm644 "plymouth.encrypt_install"  "$pkgdir/usr/lib/initcpio/install/plymouth-encrypt"
  install -Dm644 "plymouth.initcpio_hook"    "$pkgdir/usr/lib/initcpio/hooks/plymouth"
  install -Dm644 "plymouth.initcpio_install" "$pkgdir/usr/lib/initcpio/install/plymouth"
 
  for i in {gdm,kdm,sddm,lxdm,slim,lightdm}-plymouth.service; do
    install -Dm644 "$i" "$pkgdir/usr/lib/systemd/system/$i"
  done

  install -Dm644 "plymouth-start.service" "$pkgdir/usr/lib/systemd/system/plymouth-start.service"
  cp "$pkgdir/usr/share/plymouth/plymouthd.defaults" "$pkgdir/etc/plymouth/plymouthd.conf"
}


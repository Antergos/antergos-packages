# Maintainer: Guillaume Benoit <guillaume@manjaro.org>

pkgname=pamac
pkgver=2.2.1
_pkgver=2.2.1
pkgrel=3
# This is the release package so the below _gitcommit variable should (usually) be commented out.
#_gitcommit="147b8d04c8de6e401a4259edd635a770e20fc276"
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/pamac"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'yaourt' 'vte3>=0.38'
         'gtk3>=3.14' 'libnotify' 'desktop-file-utils' 'pacman>=4.2' 'pacman<4.3')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
            'lxsession: needed for authentification in Xfce, LXDE etc.')
makedepends=('gettext' 'itstool' 'vala')
backup=('etc/pamac.conf')
replaces=('pamac-aur')
conflicts=('pamac-dev')
options=(!emptydirs)
install=pamac.install

if [ "${_gitcommit}" != "" ]; then
  source=("pamac-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz")
else
  source=("pamac-$pkgver.tar.gz::$url/archive/v$_pkgver.tar.gz"
          "aur_update_check.patch::https://github.com/manjaro/pamac/commit/c578e6bee9e9275e4896ef6a361ae0f1f24ffe4e.patch"
          "locked_db_improvment.patch::https://github.com/manjaro/pamac/commit/1058576cce00b296ba8b98e81bc27b4b01a268b9.patch")
fi

sha256sums=('552041309a1af0dae44a6e1e72e299c9be671a06d8fdfa9209eb63b0aedd1dff'
            'd218d3856f132fb78b1e499007f8701ffe4b271914ea6a96e413d4cab946893c'
            'a0a50eb6eb11c9d26b3267b58966c3e55b3a6baf9af86ff5db26a17f90205b3b')

prepare() {
  if [ "$_gitcommit" != "" ]; then
    cd "$srcdir/pamac-$_gitcommit"
  else
    cd "$srcdir/pamac-$_pkgver"
  fi

  # adjust version string
  cd "$srcdir/pamac-$pkgver/src"
  patch -Np2 -i "$srcdir/aur_update_check.patch"
  patch -Np2 -i "$srcdir/locked_db_improvment.patch"
  sed -i -e "s|\"$_pkgver\"|\"$pkgver-$pkgrel\"|g" manager_window.vala
}

build() {
  if [ "$_gitcommit" != "" ]; then
    cd "$srcdir/pamac-$_gitcommit"
  else
    cd "$srcdir/pamac-$_pkgver"
  fi

  # build
  make all
}

package() {
  if [ "$_gitcommit" != "" ]; then
    cd "$srcdir/pamac-$_gitcommit"
  else
    cd "$srcdir/pamac-$_pkgver"
  fi

  mkdir -p "${pkgdir}/usr/share/icons/hicolor"
  cp -r "data/icons/"* "${pkgdir}/usr/share/icons/hicolor"
  cp -r "po/locale" "${pkgdir}/usr/share"
  install -Dm744 "src/pamac-daemon" "${pkgdir}/usr/bin/pamac-daemon"
  install -Dm755 "src/pamac-manager" "${pkgdir}/usr/bin/pamac-manager"
  install -Dm755 "src/pamac-updater" "${pkgdir}/usr/bin/pamac-updater"
  install -Dm755 "src/pamac-tray" "${pkgdir}/usr/bin/pamac-tray"
  install -Dm755 "src/pamac-refresh" "${pkgdir}/usr/bin/pamac-refresh"
  install -Dm755 "src/pamac-install" "${pkgdir}/usr/bin/pamac-install"
  install -Dm644 "data/applications/pamac-tray.desktop" "${pkgdir}/etc/xdg/autostart/pamac-tray.desktop"
  install -Dm644 "data/applications/pamac-manager.desktop" "${pkgdir}/usr/share/applications/pamac-manager.desktop"
  install -Dm644 "data/applications/pamac-updater.desktop" "${pkgdir}/usr/share/applications/pamac-updater.desktop"
  install -Dm644 "data/applications/pamac-install.desktop" "${pkgdir}/usr/share/applications/pamac-install.desktop"
  install -Dm644 "data/config/pamac.conf" "${pkgdir}/etc/pamac.conf"
  install -Dm644 "data/dbus/org.manjaro.pamac.conf" "${pkgdir}/etc/dbus-1/system.d/org.manjaro.pamac.conf"
  install -Dm644 "data/dbus/org.manjaro.pamac.service" "${pkgdir}/usr/share/dbus-1/system-services/org.manjaro.pamac.service"
  install -Dm644 "data/systemd/pamac.service" "${pkgdir}/usr/lib/systemd/system/pamac.service"
  install -Dm744 "data/networkmanager/99_update_pamac_tray" "${pkgdir}/etc/NetworkManager/dispatcher.d/99_update_pamac_tray"
  install -Dm644 "data/polkit/org.manjaro.pamac.policy" "${pkgdir}/usr/share/polkit-1/actions/org.manjaro.pamac.policy"
  install -Dm644 "data/mime/x-alpm-package.xml" "${pkgdir}/usr/share/mime/packages/x-alpm-package.xml"
}

# vim:set ts=2 sw=2 et:

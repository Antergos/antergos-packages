# Maintainer: Antergos Devs <devs@antergos.com>

pkgname=numix-icon-theme
pkgver=0.r159.7f88cbe
pkgrel=1
pkgdesc="Base icon theme from the Numix project"
arch=('any')
url='http://numixproject.org/'
license=('GPL3')
depends=('gnome-icon-theme')
makedepends=('git')
provides=("${pkgname}")
conflicts=("${pkgname}")
options=('!strip')
install="${pkgname}.install"
source=("git+https://github.com/numixproject/${pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname}

  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd ${pkgname}
  install -dm 755 "$pkgdir"/usr/share/icons
  cp -dr --no-preserve='ownership' Numix "$pkgdir"/usr/share/icons/
}

# Maintainer: Antergos Devs <devs@antergos.com>

pkgname=numix-icon-theme
pkgver=1.1825
pkgrel=1
pkgdesc='Base icon theme from the Numix project'
arch=('any')
url='http://numixproject.org/'
license=('GPL3')
depends=('gnome-icon-theme')
optdepends=('faenza-icon-theme: fallback icons'
			'breeze-icons: KDE'
			'kfaenza-icon-theme: fallback icons (KDE)')
options=('!strip')
install="${pkgname}.install"
source=("https://github.com/numixproject/${pkgname}/archive/1.1825.zip")
md5sums=('SKIP')

# ANTBS METADATA

_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='numixproject'
_monitored_repo='numix-icon-theme'

# END ANTBS METADATA


package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch_icons() {
		cd "${srcdir}/${pkgname}-${pkgver}"
		_regex='(gtk-)*go(to)*-*(\w|-)+\.svg'
		for _icon in ${1}/${2}/actions/**
		do
			_name=$(basename ${_icon})
			if [[ ${_name} =~ ${_regex} ]]; then
				_noext="${_icon/.svg}"
				_mono=$(echo "${_noext}""-symbolic.svg" | sed "s|${2}|scalable|")
				if [[ -n ${_mono} ]] && [[ -e ${_mono} ]]; then
					rm "${_icon}"
					cp -L "${_mono}" "${_icon}"
				fi
			fi
		done
	}

	for size in 16 22 24
	do
		patch_icons Numix ${size}
	done

	sed -i 's|Name=Numix Light|Name=Numix-Light|g' Numix-Light/index.theme
	sed -i 's|Inherits=Numix|Inherits=Numix,Faenza,Adwaita,gnome,hicolor|g' Numix-Light/index.theme
	sed -i 's|Inherits=gnome,hicolor|Inherits=Faenza,Adwaita,gnome,hicolor|g' Numix/index.theme

	install -dm 755 "$pkgdir"/usr/share/icons
	cp -dr --no-preserve='ownership' Numix "$pkgdir"/usr/share/icons/
	cp -dr --no-preserve='ownership' Numix-Light "$pkgdir"/usr/share/icons/
}
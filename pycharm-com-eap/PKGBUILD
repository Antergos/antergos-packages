# Maintainer: Dustin Falgout <dustin@falgout.us>
# Contributor: Gifts <gifts.antichat@gmail.com>
# Contributor: Andrey Vlasovskikh <andrey.vlasovskikh@gmail.com>

pkgname=pycharm-com-eap
_buildver=145.259
_pkgver=2016.1
_eap="False"
pkgver="${_pkgver}.${_buildver}"
pkgrel=1
pkgdesc="Powerful Python and Django IDE, Early Access Program (EAP) build. Community edition."
arch=(any)
options=('!strip')
url="http://www.jetbrains.com/pycharm/"
license=("custom")
makedepends=("wget")
provides=("pycharm-eap")
conflicts=("pycharm-eap" "pycharm-community" "pycharm-professional")
groups=("development" "IDE" "editor" "jetbrains")

if [[ "True" = "${_eap}" ]]; then
	_srcfile="pycharm-community-${_buildver}.tar.gz"
	source=("http://download.jetbrains.com/python/${_srcfile}"
			"${pkgname}.desktop")
	sha256sums=($(wget -q "${source}.sha256" && cat "${_srcfile}.sha256" | cut -f1 -d" ")
				"a4b145a776c9be7ec12a6ff1df5e3e1899bf69a5fbbb11434ffd4e312ec7251c")
else
	_srcfile="pycharm-community-${_pkgver}-RC.tar.gz"
	source=("http://download.jetbrains.com/python/${_srcfile}"
			"${pkgname}.desktop")
	sha256sums=($(wget -q "${source}.sha256" && cat "${_srcfile}.sha256" | cut -f1 -d" ")
				"a4b145a776c9be7ec12a6ff1df5e3e1899bf69a5fbbb11434ffd4e312ec7251c")
fi

package() {
  	cd "${srcdir}"
	install -dm 755 \
		"${pkgdir}/opt/${pkgname}" \
		"${pkgdir}/usr/bin/" \
		"${pkgdir}/usr/share/applications/"

	if [[ "True" = "${_eap}" ]]; then
		cp -R "${srcdir}/pycharm-community-${_buildver}/"* "${pkgdir}/opt/${pkgname}"
	else
		cp -R "${srcdir}/pycharm-community-${_pkgver}/"* "${pkgdir}/opt/${pkgname}"
	fi
	
	if [[ "i686" = "${CARCH}" ]]; then
		rm -f "${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux64.so"
		rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier64"
	fi

	sed -i "s/Version=/Version=${pkgver}/g" "${pkgname}.desktop"
	install -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/"

	ln -s "/opt/${pkgname}/bin/pycharm.sh" "${pkgdir}/usr/bin/pycharm-eap"
}

# -*- mode: bash;-*-

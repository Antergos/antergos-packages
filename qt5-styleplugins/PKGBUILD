# Maintainer: Antergos Developers <dev@antergos.com>

pkgname='qt5-styleplugins'
_srcname='qtstyleplugins'
pkgver=5.0.0
pkgrel=2
pkgdesc='Additional style plugins for Qt5'
arch=('i686' 'x86_64')
url="https://github.com/qtproject/${_srcname}"
license=('LGPL')

depends=('qt5-base' 'gconf' 'gtk2' 'libx11')
makedepends=('git')
conflicts=("${pkgname}-git")
_commit='5f2549b016b8da89160d6e2184ea83627bff92d6'
source=("${_srcname}-${_commit}.zip::${url}/archive/${_commit}.zip")
md5sums=('139cee71ab27eebef682ffbe69576b73')


build() {
	cd "${srcdir}/${_srcname}-${_commit}"

	qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib

	make
}

package() {
	cd "${srcdir}/${_srcname}-${_commit}"

	make INSTALL_ROOT="${pkgdir}" install
	rm --recursive "${pkgdir}/usr/lib/cmake"
}

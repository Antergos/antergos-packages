# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Jacob Mischka <jacob@mischka.me>
# Contributor: Pedro A. LÃ³pez-Valencia <https://aur.archlinux.org/user/vorbote>

pkgname=brave
_pkgname='browser-laptop'
pkgver=0.11.2dev
pkgrel=1
pkgdesc='Web browser that blocks ads and trackers by default.'
arch=('x86_64')
url='https://www.brave.com/'
license=('custom')
depends=('gtk2' 'nss' 'alsa-lib' 'libnotify' 'gconf' 'libxtst' 'libxss' 'desktop-file-utils'
		'ttf-font')
makedepends=('npm' 'git' 'python2')
provides=('brave-browser')
source=("https://github.com/brave/${_pkgname}/archive/v${pkgver}dev.tar.gz"
		'brave.desktop')
md5sums=('a48762e20c308860765c238d5d89f782'
		'816c6930c632ef34f9e530f9e5bcf96f')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='brave'
_monitored_repo='browser-laptop'
_monitored_match_pattern='/(v(\d{1,2}\.){1,2}\ddev$)/'
# END ANTBS METADATA


build() {
	cd "${srcdir}/browser-laptop-${pkgver}dev"
	PYTHON='/usr/bin/python2' HOME="${srcdir}" npm install
	HOME="${srcdir}" CHANNEL='dev' npm run build-package
}

package() {
	cd "${srcdir}/browser-laptop-${pkgver}dev"

	install -dm0755 "${pkgdir}/usr/lib"

	cp -a --reflink=auto 'Brave-linux-x64' "${pkgdir}/usr/lib/brave"

	install -Dm0644 "${srcdir}/brave.desktop" "${pkgdir}/usr/share/applications/brave.desktop"

	install -dm0755 "${pkgdir}/usr/bin"

	ln -s '/usr/lib/brave/brave' "${pkgdir}/usr/bin/brave"

	install -Dm0644 'res/app.png' "${pkgdir}/usr/share/pixmaps/brave.png"

	install -dm0755 "${pkgdir}/usr/share/licenses/brave"

	cp --reflink=auto "${srcdir}/License.txt"  "${pkgdir}/usr/share/licenses/brave/MPL2"

	mv "${pkgdir}"/usr/lib/brave/{LICENSE,LICENSES.chromium.html} "${pkgdir}/usr/share/licenses/brave/"
}

# -*- mode: bash;-*-
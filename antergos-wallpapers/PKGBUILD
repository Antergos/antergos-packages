# Maintainer: Alexandre Filgueira <faidoc@gmail.com>

pkgname=antergos-wallpapers
pkgver=0.6
pkgrel=2
pkgdesc='The default wallpapers for Antergos'
arch=('any')
url='http://www.antergos.com'
license=('CCPL:by-nc-sa')
source=("http://antergos.info/${pkgname}-${pkgver}.zip")
sha256sums=('e1044c42519f8bb116387b453121724adfa0be46ea76b3f153013db7d194a536')

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	install -d "${pkgdir}/usr/share/antergos/wallpapers"
	install -m644 *.jpg "${pkgdir}/usr/share/antergos/wallpapers/"
	install -m644 *.png "${pkgdir}/usr/share/antergos/wallpapers/"

	install -d "${pkgdir}/usr/share/gnome-background-properties"
	install -m644 antergos-backgrounds-4-3.xml "${pkgdir}/usr/share/gnome-background-properties/"

	# Cinnamon
	_bg_path="${pkgdir}/usr/share/cinnamon-background-properties/antergos.xml"

	install -d "${pkgdir}/usr/share/cinnamon-background-properties"
	cp --no-preserve=ownership antergos-backgrounds-4-3.xml "${_bg_path}"
	sed -i 's|gnome-wp-list|cinnamon-wp-list|g' "${_bg_path}"
}

# -*- mode: bash;-*-

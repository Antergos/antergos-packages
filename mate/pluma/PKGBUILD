# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=pluma
pkgver=1.14.0
pkgrel=2
pkgdesc="A powerful text editor for the MATE Desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'enchant' 'gtk3' 'gtksourceview3' 'iso-codes'
		 'libsm' 'mate-desktop>=1.14' 'zenity')
makedepends=('mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz"
		'pluma_diasable-python-plugins.patch')
md5sums=('8386579dac1e16a4d93dfc5f668a206e'
         '09b573a9a7f70dd15133b1201d41678c')
install=${pkgname}.install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# disable non working python plugins for gtk3
	patch -Npr 1 ../pluma_diasable-python-plugins.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/${pkgname} \
		--disable-static \
		--enable-gtk-doc-html \
		--enable-gvfs-metadata \
		--disable-python \
		--disable-schemas-compile \
		--with-gtk=3.0
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 
 

# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-power-manager
pkgver=1.14.0
pkgrel=2
pkgdesc="Power management tool for the MATE desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'gtk3' 'libcanberra' 'libgnome-keyring'
		 'libnotify' 'libunique3' 'mate-desktop>=1.14' 'upower')
makedepends=('docbook2x' 'docbook-xml' 'mate-common' 'mate-panel' 'xmlto' 'yelp-tools')
optdepends=('mate-panel: Set brightness and inhibit power management from the panel'
			'yelp: for reading MATE help documents')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
groups=('mate-extra')
sha1sums=('286ac97658d18ea1fffa6e0a692a877727a44e41')
install=${pkgname}.install

# BEGIN ANTBS METADATA

_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='mate-desktop'
_monitored_repo='mate-power-manager'

# END ANTBS METADATA

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Work around a problem where the .sgml fails to parse.
	sed -e 's:@HAVE_DOCBOOK2MAN_TRUE@.*::' -i man/Makefile.in
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/${pkgname} \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--sbindir=/usr/bin \
		--enable-applets \
		--enable-unique \
		--with-gtk=3.0 \
		--disable-strict
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 
 

# -*- mode: bash;-*-
 

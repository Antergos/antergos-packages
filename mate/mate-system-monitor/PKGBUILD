# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-system-monitor
pkgver=1.14.0
pkgrel=2
pkgdesc="A system monitor for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'dbus-glib' 'glibmm' 'gtkmm' 'gtkmm3' 'libgtop' 'libwnck'
		 'libwnck3' 'librsvg')
makedepends=('docbook2x' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
md5sums=('a76a853a473fcffe32bedd401d61738a')
install=${pkgname}.install


# BEGIN ANTBS METADATA

_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='mate-desktop'
_monitored_repo='mate-system-monitor'

# END ANTBS METADATA

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/${pkgname} \
		--localstatedir=/var \
		--with-gtk=3.0 \
		--disable-static
	make
}


package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 
 

# -*- mode: bash;-*-

# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-screensaver
pkgver=1.14.1
pkgrel=2
pkgdesc='Screensaver for MATE'
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'libmatekbd>=1.14' 'libnotify' 'libxss' 'mate-desktop>=1.14'
		 'mate-menus>=1.14' 'mate-session-manager>=1.14')
makedepends=('mate-common' 'xmlto')
optdepends=('mate-power-manager: Add power management support.'
			'mate-screensaver-hacks: Additional screensavers.'
			'rss-glx: Really slick screensavers.')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
md5sums=('fa6f55f00f33fb6fca007c30dd1d4736')
install=${pkgname}.install

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='mate-desktop'
_monitored_repo='mate-screensaver'
_monitored_match_pattern='1.14'
# END ANTBS METADATA

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/${pkgname} \
		--sysconfdir=/etc \
		--with-xscreensaverdir=/usr/share/xscreensaver/config \
		--with-xscreensaverhackdir=/usr/lib/xscreensaver \
		--with-mit-ext \
		--with-libnotify \
		--enable-locking \
		--with-gtk=3.0 \
		--without-console-kit \
		--disable-static
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 
 

# -*- mode: bash;-*-
 

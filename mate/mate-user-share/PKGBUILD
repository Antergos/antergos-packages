# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-user-share
pkgver=1.14.0
pkgrel=2
pkgdesc="User level public file sharing via WebDAV for MATE."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'libunique3' 'mod_dnssd' 'gtk3')
makedepends=('caja>=1.14' 'libcanberra' 'libnotify' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('95491485a0769f252e068ed2256f1a3386330094')
install=${pkgname}.install

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='mate-desktop'
_monitored_repo='mate-user-share'
_monitored_match_pattern='1.14'
# END ANTBS METADATA

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--libexec=/usr/lib/${pkgname} \
		--sysconfdir=/etc \
		--disable-static \
		--disable-bluetooth \
		--with-gtk=3.0
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make  DESTDIR="${pkgdir}" install
	rm -f "${pkgdir}/usr/share/mate-user-share/dav_user_2.0.conf"
}
 
 

# -*- mode: bash;-*-
 

# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-media
pkgver=1.14.0
pkgrel=2
url="http://mate-desktop.org"
pkgdesc="MATE Media Tools"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libcanberra' 'libmatemixer' 'libunique3' 'mate-desktop>=1.14')
makedepends=('mate-common')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
groups=('mate-extra')
replaces=("${pkgname}-gstreamer" "${pkgname}-pulseaudio")
conflicts=("${pkgname}-gstreamer" "${pkgname}-pulseaudio")
md5sums=('aedfc6cdba91d5a342154acda9229bea')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/${pkgname} \
		--localstatedir=/var \
		--with-gtk=3.0 \
		--disable-static
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 
 

# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-utils
pkgver=1.14.0
pkgrel=2
pkgdesc="Common MATE utilities for viewing disk usage, logs, and fonts, taking screenshots, managing dictionaries, and searching files"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'gtk3' 'libcanberra' 'libgtop' 'libsm' 'zlib')
makedepends=('mate-common' 'mate-panel>=1.14' 'rarian' 'yelp-tools')
optdepends=('mate-panel: Look up words in dictionaries from the panel'
			'yelp: for reading MATE help documents')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
md5sums=('79a46d5600de21d760690e9f544c66e8')
install=${pkgname}.install

# BEGIN ANTBS METADATA

_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='mate-desktop'
_monitored_repo='mate-utils'

# END ANTBS METADATA

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/${pkgname} \
		--with-gtk=3.0 \
		--sysconfdir=/etc \
		--enable-deprecated \
		--disable-maintainer-flags \
		--disable-static
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 
 

# -*- mode: bash;-*-

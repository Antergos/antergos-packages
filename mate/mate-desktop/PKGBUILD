# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-desktop
pkgver=1.14.1
pkgrel=2
pkgdesc="Library with common API for various MATE modules"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('dconf' 'gtk3' 'startup-notification' 'gobject-introspection')
makedepends=('mate-common' 'yelp-tools')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
md5sums=('3d8d55e228b4ab82cf33004e314820e0')
install=${pkgname}.install

# BEGIN ANTBS METADATA

_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='mate-desktop'
_monitored_repo='mate-desktop'

# END ANTBS METADATA

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--with-gtk=3.0 \
		--disable-static \
		--disable-schemas-compile \
		--enable-gtk-doc
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 
 

# -*- mode: bash;-*-

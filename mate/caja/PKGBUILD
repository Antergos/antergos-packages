# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=caja
pkgver=1.14.1
pkgrel=2
pkgdesc="File manager for the MATE desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus' 'desktop-file-utils' 'exempi' 'gtk3' 'gvfs' 'libexif' 'python-gobject'
		'libunique3' 'libxt' 'mate-desktop>=1.14' 'startup-notification')
makedepends=('gobject-introspection' 'mate-common')
optdepends=('gstreamer: automatic media playback when mouseover'
			'gvfs-smb: To connect to Samba/Windows shares')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
md5sums=('b43639c01a70c9c3cfbca9005656b073')
install=${pkgname}.install

# BEGIN ANTBS METADATA

_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='mate-desktop'
_monitored_repo='caja'

# END ANTBS METADATA

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/${pkgname} \
		--with-gtk=3.0 \
		--enable-unique \
		--enable-introspection \
		--disable-static \
		--disable-update-mimedb
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 
 

# -*- mode: bash;-*-
 

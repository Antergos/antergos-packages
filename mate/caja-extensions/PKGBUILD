# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgbase=caja-extensions
pkgname=(caja-extensions-common caja-gksu caja-image-converter caja-open-terminal caja-sendto caja-share)
pkgver=1.14.0
pkgrel=2
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja>=1.14' 'dbus-glib' 'gksu' 'gtk3' 'gupnp' 'imagemagick' 'samba' 'python-gobject')
makedepends=('mate-common')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgbase}-${pkgver}.tar.xz")
md5sums=('d43891792beef546cc87b38914f64572')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='mate-desktop'
_monitored_repo='caja-extensions'
_monitored_match_pattern='1.14'
# END ANTBS METADATA


build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	PYTHON=/usr/bin/python2 ./configure \
		--prefix=/usr \
		--with-gtk=3.0
	make
}

package_caja-extensions-common() {
	pkgdesc="Caja extensions (common files)"
	depends=()

	cd "${srcdir}/${pkgbase}-${pkgver}"
	make -C po DESTDIR="${pkgdir}" install
}

package_caja-gksu() {
	pkgdesc="Privilege granting extension for Caja using gksu"
	depends=('caja' 'caja-extensions-common' 'gksu')
	groups=('mate-extra')

	cd "${srcdir}/${pkgbase}-${pkgver}"
	make -C gksu DESTDIR="${pkgdir}" install
}

package_caja-image-converter() {
	pkgdesc="A Caja extension for simple image conversions"
	depends=('caja' 'caja-extensions-common' 'imagemagick')
	groups=('mate-extra')

	cd "${srcdir}/${pkgbase}-${pkgver}"
	make -C image-converter DESTDIR="${pkgdir}" install
}

package_caja-open-terminal() {
	pkgdesc="A Caja extension for opening terminals in arbitrary local paths"
	depends=('caja' 'caja-extensions-common')
	groups=('mate-extra')
	install=caja-open-terminal.install

	cd "${srcdir}/${pkgbase}-${pkgver}"
	make -C open-terminal DESTDIR="${pkgdir}" install
}

package_caja-sendto() {
	pkgdesc="A Caja extension for sending files"
	depends=('caja' 'caja-extensions-common' 'dbus-glib' 'gupnp')
	groups=('mate-extra')
	optdepends=('pidgin: Support for sending to instant messaging networks.'
				'gajim: Support for sending to Jabber.')
	install=caja-sendto.install

	cd "${srcdir}/${pkgbase}-${pkgver}"
	make -C sendto DESTDIR="${pkgdir}" install
}


package_caja-share() {
	pkgdesc="A Caja extension to quickly share a folder"
	depends=('caja' 'caja-extensions-common' 'samba')
	groups=('mate-extra')
	
	cd "${srcdir}/${pkgbase}-${pkgver}"
	make -C share DESTDIR="${pkgdir}" install
}

# -*- mode: bash;-*-
 

# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-applets
pkgver=1.16.0
pkgrel=3
pkgdesc="Applets for MATE panel"
arch=('i686' 'x86_64')
url="http://mate-desktop.org"
license=('GPL')
depends=('gtksourceview3' 'libgtop' 'libnotify' 'mate-panel>=1.14'
		 'polkit' 'python2-dbus' 'python2-gobject' 'upower')
makedepends=('docbook2x' 'docbook-xsl' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
replaces=('mate-netspeed' 'mate-netspeed-gtk3')
conflicts=('mate-netspeed' 'mate-netspeed-gtk3')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
md5sums=('e316ade06cba447908120eee07b777dcfae436cd')
install=${pkgname}.install

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='mate-desktop'
_monitored_repo='mate-applets'
_monitored_match_pattern='1.14'
# END ANTBS METADATA

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	grep -RZl 'env python' | xargs -0 -l sed -i 's/env python/env python2/'
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	PYTHON=/usr/bin/python2 ./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/${pkgname} \
		--enable-polkit \
		--enable-ipv6 \
		--with-gtk=3.0 \
		--disable-static
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 

# -*- mode: bash;-*-
 

# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-settings-daemon
pkgver=1.14.0
pkgrel=2
pkgdesc="The MATE Settings Daemon"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'fontconfig' 'gtk3' 'libcanberra-pulse'
		 'libmatekbd>=1.14' 'libmatemixer' 'libnotify' 'libxt'
		 'mate-desktop>=1.14' 'nss' 'polkit')
makedepends=('mate-common')
groups=('mate')
replaces=("${pkgname}-gstreamer" "${pkgname}-pulseaudio")
conflicts=("${pkgname}-gstreamer" "${pkgname}-pulseaudio")
source=("http://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
md5sums=('1195bfb30c1468a9061f8e9aa07260e8')
install=${pkgname}.install

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='mate-desktop'
_monitored_repo='mate-settings-daemon'
_monitored_match_pattern='1.14'
# END ANTBS METADATA

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/${pkgname} \
		--sysconfdir=/etc \
		--with-gtk=3.0 \
		--enable-polkit \
		--disable-static
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
 
 

# -*- mode: bash;-*-
 

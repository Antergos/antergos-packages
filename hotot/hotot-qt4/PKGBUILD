# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=hotot-qt
pkgver=0.9.8.14
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca). Qt4 frontend."
arch=('i686' 'x86_64')
url="http://www.hotot.org/"
license=('LGPL3')
depends=('hotot-data' 'qtwebkit' 'qt4')
makedepends=('cmake' 'intltool')
install="hotot-qt.install"
source=("https://github.com/lyricat/Hotot/archive/$pkgver.tar.gz")
md5sums=('7437f5132a50f7239e1b4bd09f410a17')

build() {
  cd "${srcdir}/Hotot-${pkgver}"
  mkdir -p build
  cd build
  [[ -f CMakeCache.txt ]] && rm -v CMakeCache.txt
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DWITH_GTK=Off -DWITH_CHROME=Off -DWITH_QT=On \
    -DWITH_QT5=Off
  make
  make install DESTDIR="${PWD}/pkg"
}

package() {
  cd "${srcdir}/Hotot-${pkgver}/build/pkg"

  install -Dm755 usr/bin/hotot-qt "$pkgdir/usr/bin/hotot-qt"
  install -Dm644 usr/share/applications/hotot-qt.desktop \
    "$pkgdir/usr/share/applications/hotot-qt.desktop"
}

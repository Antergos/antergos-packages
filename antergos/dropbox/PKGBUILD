# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Tom < tomgparchaur at gmail dot com >
# Contributor: David Manouchehri <d@32t.ca>

pkgname=dropbox
pkgver=16.4.30
pkgrel=1
pkgdesc="A free service that lets you bring your photos, docs, and videos anywhere and share them easily."
arch=("i686" "x86_64")
url="http://www.dropbox.com"
license=(custom)
depends=("dbus-glib" "libsm" "libxslt")
conflicts=("dropbox-experimental")
groups=('cloud-storage')
options=('!strip' '!upx')
install="dropbox.install"


source_i686=("https://d1ilhw0800yew8.cloudfront.net/client/dropbox-lnx.x86-${pkgver}.tar.gz")
source_x86_64=("https://d1ilhw0800yew8.cloudfront.net/client/dropbox-lnx.x86_64-${pkgver}.tar.gz")
source+=("dropbox.png"
		"dropbox.desktop"
		"terms.txt"
		"dropbox.service"
		"dropbox@.service")
md5sums=('2be5a57e477adf82a7d9b7fe77f22086'
         'f3350cc0fb7b5f4fb6f2c0adda7e1260'
         '9e140f8de1bab8f26165068bb2c5e8b4'
         '2301498f15f87836ea9923269e2f23be'
         'bf7692b7f5c823a5ef47deb10ce3b0dc')
md5sums_i686=('a6fcaae5335027b226aac3c269471151')
md5sums_x86_64=('15c9da8aa1721b1212617453b0a32917')

#https://www.dropbox.com/release_notes/rss.xml

package() {
	install -d "$pkgdir"/opt
	cp -R "$srcdir"/.dropbox-dist/dropbox-lnx.x86-$pkgver "$pkgdir"/opt/dropbox > /dev/null 2>&1 || \
	cp -R "$srcdir"/.dropbox-dist/dropbox-lnx.x86_64-$pkgver "$pkgdir"/opt/dropbox

	install -d "$pkgdir"/usr/bin

	install -Dm644 "$srcdir"/dropbox.desktop "$pkgdir"/usr/share/applications/dropbox.desktop
	install -Dm644 "$srcdir"/dropbox.png "$pkgdir"/usr/share/pixmaps/dropbox.png
	install -Dm644 "$srcdir"/terms.txt "$pkgdir"/usr/share/licenses/$pkgname/terms.txt
	install -Dm644 "$srcdir"/dropbox.service "$pkgdir"/usr/lib/systemd/system/dropbox.service
	install -Dm644 "$srcdir"/dropbox@.service "$pkgdir"/usr/lib/systemd/system/dropbox@.service

	rm -f "$pkgdir"/opt/dropbox/library.zip
	ln -s dropbox "$pkgdir"/opt/dropbox/library.zip
}
# -*- mode: bash;-*-

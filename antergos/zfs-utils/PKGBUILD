# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=zfs-utils
_pkgver=0.7.0rc3
_rev=2609
_kern=4.9.6_1
pkgver="${_pkgver}.r${_rev}.${_kern}"
pkgrel=1
pkgdesc="Kernel module support files for the Zettabyte File System."
url="http://zfsonlinux.org/"
license=('CDDL')
arch=("i686" "x86_64")
groups=("system")
depends=("spl=${pkgver/2609/1017}")
conflicts=("zfs-utils-dkms-git")
_commit='10a3f8885904558c2a406151c4cf960371c34934'
source=("https://github.com/zfsonlinux/zfs-utils/archive/${_commit}.zip"
		"zfs-utils.bash-completion-r1"
		"zfs-utils.initcpio.install"
		"zfs-utils.initcpio.hook")
md5sums=('f91a42244dfc05e17cc97e40d68b91cd'
         '9ddb0c8a94861f929d0fa741fdc49950'
         '110b48d872a7a7d2884496267f92418a'
         '62ba32c6853c315ff231c396cf8bfdbb')


build() {
	cd "${srcdir}/zfs-${_commit}"
	./autogen.sh

	{ [[ "${CARCH}" == i686 ]] && _i686_target="--target=i686-linux-gnu"; } || _i686_target=""

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--with-mounthelperdir=/usr/bin \
		--libdir=/usr/lib \
		--datadir=/usr/share \
		--includedir=/usr/include \
		--with-udevdir=/usr/lib/udev \
		--libexecdir=/usr/lib/zfs \
		--with-config=user \
		${_i686_target}
	make
}

package() {
	cd "${srcdir}/zfs-${_commit}"
	make DESTDIR="${pkgdir}" install

	# Remove uneeded files
	rm -r "${pkgdir}"/etc/init.d
	rm -r "${pkgdir}"/usr/lib/dracut

	install -D -m644 "${srcdir}"/zfs-utils.initcpio.hook "${pkgdir}"/usr/lib/initcpio/hooks/zfs
	install -D -m644 "${srcdir}"/zfs-utils.initcpio.install "${pkgdir}"/usr/lib/initcpio/install/zfs
	install -D -m644 "${srcdir}"/zfs-utils.bash-completion-r1 "${pkgdir}"/usr/share/bash-completion/completions/zfs
}
# -*- mode: bash;-*-

# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=zfs-utils
_pkgver=0.8.0rc5
pkgver=0.8.0rc5
pkgrel=1
pkgdesc="Kernel module support files for the Zettabyte File System."
url="http://zfsonlinux.org/"
license=('CDDL')
arch=("x86_64")
groups=("system" "zfs")
conflicts=("zfs-utils-git")
_commit=''
_tag="zfs-${_pkgver/rc5/-rc5}"
source=(
	"https://github.com/zfsonlinux/zfs/archive/${_tag}.zip"
	"zfs-utils.bash-completion-r1"
	"zfs-utils.initcpio.install"
	"zfs-utils.initcpio.hook"
)
md5sums=('8341bf06845e0a71b2555affc125b756'
         '9ddb0c8a94861f929d0fa741fdc49950'
         '73527e326fbbd1bc3d4331bbba0af5a8'
         '2d4dd00dd671598c26b5d054bd63d6f5')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='zfsonlinux'
_monitored_repo='zfs'
_monitored_match_pattern='/(?<=zfs-|spl-)\S+/'
# END ANTBS METADATA

prepare() {
	cd "${srcdir}/zfs-${_tag}"
}

build() {
    cd "${srcdir}/zfs-${_tag}"
    ./autogen.sh
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --with-mounthelperdir=/usr/bin \
        --libdir=/usr/lib \
        --datadir=/usr/share \
        --includedir=/usr/include \
        --with-udevdir=/usr/lib/udev \
        --libexecdir=/usr/lib/zfs \
        --with-config=user \
        --enable-systemd
    make
}

package() {
    cd "${srcdir}/zfs-${_tag}"
    make DESTDIR="${pkgdir}" install

    # Remove uneeded files
    rm -r "${pkgdir}"/etc/init.d
    rm -r "${pkgdir}"/usr/lib/dracut

    install -D -m644 "${srcdir}"/zfs-utils.initcpio.hook "${pkgdir}"/usr/lib/initcpio/hooks/zfs
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.install "${pkgdir}"/usr/lib/initcpio/install/zfs
    install -D -m644 "${srcdir}"/zfs-utils.bash-completion-r1 "${pkgdir}"/usr/share/bash-completion/completions/zfs
}

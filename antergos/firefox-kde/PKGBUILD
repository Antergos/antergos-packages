# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Weng Xuetian <wengxt@gmail.com>
# Contributor: Thaodan <theodorstormgrade@gmail.com>

_pkgname=firefox
pkgname=firefox-kde
pkgver=52.0.1
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org with openSUSE KDE integration patches."
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url="https://build.opensuse.org/package/show/mozilla:Factory/MozillaFirefox"
groups=('browser')

depends=('alsa-lib'        'dbus-glib'  'ffmpeg'  'gtk2'
        'gtk3'            'hunspell'   'icu'     'kmozillahelper'
        'libevent'        'libvpx'     'libxt'   'mime-types'
        'mozilla-common'  'nss'        'sqlite'  'startup-notification'
        'ttf-font')

makedepends=('autoconf2.13'  'cargo'      'diffutils'        'gconf'
			'imake'         'inetutils'  'libpulse'         'mesa'
			'python2'       'unzip'      'xorg-server-xvfb' 'yasm'
			'zip')

optdepends=('networkmanager: Location detection via available WiFi networks'
			'upower: Battery API'
			'speech-dispatcher: Text-to-Speech')

provides=("firefox=${pkgver}")
conflicts=('firefox')
options=('!emptydirs' '!makeflags')
_patchrev=ce8a98f8d8d7
_patchurl="http://www.rosenauer.org/hg/mozilla/raw-file/${_patchrev}"
_archpkg_git_url='https://git.archlinux.org/svntogit/packages.git/plain/trunk'

source=("https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/${_pkgname}-${pkgver}.source.tar.xz"
		"fix-wifi-scanner.diff::${_archpkg_git_url}/fix-wifi-scanner.diff?h=packages/firefox"
		"firefox-install-dir.patch::${_archpkg_git_url}/firefox-install-dir.patch?h=packages/firefox"
		'vendor.js'
		"rust-i686.patch::${_archpkg_git_url}/rust-i686.patch?h=packages/firefox"
		'mozconfig'
		'firefox.desktop'
		'kde.js'
		# Firefox patchset
		$_patchurl/firefox-branded-icons.patch
		$_patchurl/firefox-kde.patch
		$_patchurl/firefox-no-default-ualocale.patch
		# Gecko/toolkit patchset
		$_patchurl/mozilla-kde.patch
		$_patchurl/mozilla-language.patch
		$_patchurl/mozilla-nongnome-proxies.patch
		$_patchurl/mozilla-reduce-files-per-UnifiedBindings.patch)
md5sums=('5374ed76d3bd5a2c1254b2d3997768a9'
         'e2396b9918aa602427f80d48caf319b4'
         'dbf14588e85812ee769bd735823a0146'
         'aa9f776d2187cba09de65cbb02b39ca0'
         '40b197fcf8c855b3902414f4d591b18b'
         '935111ca236a798f9c6b4b9f8ffb1442'
         '9b02198df96be08f2a0a323ac2e6c2a2'
         '75df0f88cc7a7fa7d522459e4ff82cc5'
         '3b3eb8c2747429887e4a03537932eac5'
         '688b522c10f805261f07c085848487eb'
         '1fad9a988826d69fe712ea973e43f6da'
         '4ac0c221feeee260172acf50581b2246'
         'fa6ac817f576b486419b5f308116a7cd'
         '0c684360f1df4536512d51873c1d243d'
         '4d240471a642270accd515199ca698e4')

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Antergos Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact info@antergos.com for
# more information.
_google_='AIzaSyBPgpho0q0siP2tggJwIsDx8ykpp-Y76Xk'

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Antergos Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact info@antergos.com for
# more information.
_mozilla_='fa35d4d6-6529-436f-b3a1-8b9af187eca2'


prepare() {
	cd "${_pkgname}-${pkgver}"

	cp ../mozconfig .mozconfig
	patch -Np1 -i ../firefox-install-dir.patch

	# https://bugzilla.mozilla.org/show_bug.cgi?id=1314968
	patch -Np1 -i ../fix-wifi-scanner.diff

	# Build with the rust targets we actually ship
	patch -Np1 -i ../rust-i686.patch

	# KDE Integration Patches
	patch -Np1 -i "${srcdir}/mozilla-nongnome-proxies.patch"
	patch -Np1 -i "${srcdir}/mozilla-kde.patch"
	patch -Np1 -i "${srcdir}/mozilla-language.patch"
	patch -Np1 -i "${srcdir}/firefox-kde.patch"
	patch -Np1 -i "${srcdir}/firefox-no-default-ualocale.patch"
	patch -Np1 -i "${srcdir}/firefox-branded-icons.patch"
	patch -Np1 -i "${srcdir}/mozilla-reduce-files-per-UnifiedBindings.patch"

	echo -n "$_google_" >google-api-key
	echo "ac_add_options --with-google-api-keyfile=\"$PWD/google-api-key\"" >>.mozconfig

	echo -n "$_mozilla_" >mozilla-api-key
	echo "ac_add_options --with-mozilla-api-keyfile=\"$PWD/mozilla-api-key\"" >>.mozconfig

	[[ 'i686' = "${CARCH}" ]] && {
		_python="/32build/root${_python}"
		echo 'ac_add_options --target=i686-unknown-linux-gnu' >>.mozconfig
		echo 'ac_add_options --host=i686-unknown-linux-gnu' >>.mozconfig
	}

	_python=/usr/bin/python2

	mkdir "$srcdir/path"

	ln -s ${_python} "$srcdir/path/python"
}


build() {
	cd "${_pkgname}-${pkgver}" || cd "${_pkgname}-${_majorver}"

	# _FORTIFY_SOURCE causes configure failures
	CPPFLAGS+=" -O2"

	# Hardening
	LDFLAGS+=" -Wl,-z,now"

	# GCC 6
	CXXFLAGS+=" -fno-delete-null-pointer-checks -fno-schedule-insns2"

	export PATH="${srcdir}/path:$PATH"

	# Do PGO
	# xvfb-run -a -s "-extension GLX -screen 0 1280x1024x24" \
	# make -f client.mk build MOZ_PGO=1
	make -f client.mk build
}


package() {
	cd "${_pkgname}-${pkgver}"

	[[ "$CARCH" == "i686" ]] && cp "$srcdir/kde.js" obj-i686-*-linux-gnu/dist/bin/defaults/pref
	[[ "$CARCH" == "x86_64" ]] && cp "$srcdir/kde.js" obj-x86_64-*-linux-gnu/dist/bin/defaults/pref

	make -f client.mk DESTDIR="${pkgdir}" INSTALL_SDK= install

	install -Dm644 ../vendor.js "${pkgdir}/usr/lib/${_pkgname}/browser/defaults/preferences/vendor.js"
	install -Dm644 ../kde.js "${pkgdir}/usr/lib/firefox/browser/defaults/preferences/kde.js"

	for i in 16 22 24 32 48 256; do
		install -Dm644 "browser/branding/official/default${i}.png" \
			"${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/${_pkgname}.png"
	done

	_branding='browser/branding/official'
	_hicolor="${pkgdir}/usr/share/icons/hicolor"
	install -Dm644 "${_branding}/content/icon64.png" "${_hicolor}/64x64/apps/${_pkgname}.png"
	install -Dm644 "${_branding}/mozicon128.png" "${_hicolor}/128x128/apps/${_pkgname}.png"
	install -Dm644 "${_branding}/content/about-logo.png" "${_hicolor}/192x192/apps/${_pkgname}.png"
	install -Dm644 "${_branding}/content/about-logo@2x.png" "${_hicolor}/384x384/apps/${_pkgname}.png"

	install -Dm644 ../firefox.desktop "${pkgdir}/usr/share/applications/firefox.desktop"

	# Use system-provided dictionaries
	rm -rf "${pkgdir}/usr/lib/${pkgname}"/{dictionaries,hyphenation}
	ln -s /usr/share/hunspell "${pkgdir}/usr/lib/${_pkgname}/dictionaries"
	ln -s /usr/share/hyphen "${pkgdir}/usr/lib/${_pkgname}/hyphenation"

	# Replace duplicate binary with symlink
	# https://bugzilla.mozilla.org/show_bug.cgi?id=658850
	ln -sf firefox "${pkgdir}/usr/lib/${_pkgname}/firefox-bin"
}

# -*- mode: bash;-*-

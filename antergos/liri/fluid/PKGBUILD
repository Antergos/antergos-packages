# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=fluid
pkgver=0.9.0
pkgrel=1
pkgdesc='Cross-platform QtQuick components for building fluid and dynamic applications.'
arch=('x86_64')
url='https://github.com/lirios/fluid'
license=('MPL2')
depends=('qt5-quickcontrols2' 'qt5-graphicaleffects' 'qt5-svg')
makedepends=('extra-cmake-modules')
conflicts=('qml-material' 'fluid-git')
replaces=('qml-material')
groups=('liri')
_gitname="${pkgname}"
source=("https://github.com/lirios/${_gitname}/archive/v${pkgver}.tar.gz")
md5sums=('30f576d96bfa56b8b5d925585523d024')


# BEGIN ANTBS METADATA
_is_monitored='False'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='lirios'
_monitored_repo='fluid'
# END ANTBS METADATA


prepare() {
	mkdir -p build
	cd ${_gitname}-${pkgver}
	./scripts/fetch_icons.sh
	cd -
}

build() {
	cd build
	cmake ../${_gitname}-${pkgver} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_INSTALL_LIBDIR=lib \
		-DKDE_INSTALL_LIBEXECDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}

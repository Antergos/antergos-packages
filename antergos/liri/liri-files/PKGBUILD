# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=liri-files
pkgver=0.1.0
pkgrel=1
pkgdesc='File manager for Liri OS'
arch=('x86_64')
url='https://github.com/lirios/files'
license=('GPL3')
depends=('fluid-git' 'taglib')
makedepends=('extra-cmake-modules' 'qt5-tools')
conflicts=('liri-files-git')
groups=('liri')
_gitname="${pkgname/liri-}"
source=("https://github.com/lirios/${_gitname}/archive/v${pkgver}.tar.gz")
md5sums=('7c491cd2718d4f051c73642e1aaaa009')


# BEGIN ANTBS METADATA
_is_monitored='False'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='lirios'
_monitored_repo='liri-files'
# END ANTBS METADATA


prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake ../"${_gitname}-${pkgver}" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_INSTALL_LIBDIR=lib \
		-DKDE_INSTALL_LIBEXECDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}

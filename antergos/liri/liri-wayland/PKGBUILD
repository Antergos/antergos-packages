# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=liri-wayland
pkgver=0.9.0.1
pkgrel=1
pkgdesc="QtWayland additions and QPA plugin"
arch=('any')
url='https://github.com/lirios/wayland'
license=('GPL3')
depends=('systemd' 'wayland-protocols' 'libdrm' 'libinput' 'qt5-declarative' 'qt5-wayland'
         'xkeyboard-config' 'libxkbcommon' 'glib2' 'fontconfig' 'freetype2')
makedepends=('extra-cmake-modules' 'xcb-util-cursor' 'libxcursor')
conflicts=('greenisland' 'greenisland-git')
replaces=('greenisland')
groups=('liri')
_gitname="${pkgname/liri-}"
source=("https://github.com/lirios/${_gitname}/archive/v${pkgver}.tar.gz")
md5sums=('ca476e2729e9caaba951d9aa81ac3c7a')


# BEGIN ANTBS METADATA
_is_monitored='False'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='lirios'
_monitored_repo='liri-wayland'
# END ANTBS METADATA


prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake ../"${_gitname}-${pkgver}" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_INSTALL_LIBDIR=lib \
		-DKDE_INSTALL_LIBEXECDIR=lib \
		-DBUILD_HACK=ON
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}

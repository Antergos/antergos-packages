# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=liri-workspace
pkgver=0.9.0
pkgrel=1
pkgdesc="Liri workspace, programs and plugins"
arch=('any')
url='https://github.com/lirios/workspace'
license=('GPL3')
depends=('qt5-tools' 'qt5-gstreamer' 'greenisland' 'vibe' 'glib2' 'dconf'
		'numix-icon-theme-square' 'liri-wallpapers' 'ttf-dejavu' 'ttf-droid' 'noto-fonts')
makedepends=('extra-cmake-modules' 'boost')
conflicts=('hawaii-workspace-git' 'liri-workspace')
groups=('liri')
_gitname="${pkgname/liri-}"
source=("https://github.com/lirios/${_gitname}/archive/v${pkgver}.tar.gz")
md5sums=('8969143c6e50c2bb6cc4f69d963ca509')


# BEGIN ANTBS METADATA
_is_monitored='False'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='lirios'
_monitored_repo='liri-workspace'
# END ANTBS METADATA


prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake ../"${_gitname}-${pkgver}" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_INSTALL_LIBDIR=lib \
		-DKDE_INSTALL_LIBEXECDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}

# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=vibe
pkgver=0.9.0
pkgrel=1
pkgdesc="A collection of core classes used throughout Liri"
arch=('any')
url='https://github.com/lirios/vibe'
license=('LGPL3')
depends=('qt5-base' 'qt5-declarative' 'qt5-tools' 'kwallet' 'solid' 'networkmanager-qt'
		'modemmanager-qt' 'pulseaudio' 'libqtxdg' 'fluid')
makedepends=('extra-cmake-modules')
conflicts=('libhawaii' 'libhawaii-git' 'libpapyros' 'libpapyros-git')
replaces=('libhawaii' 'libpapyros')
groups=('liri')
_gitname="${pkgname}"
source=("https://github.com/lirios/${_gitname}/archive/v${pkgver}.tar.gz")
md5sums=('4a715c7744d65ce6c2cdbea43e3bca03')


# BEGIN ANTBS METADATA
_is_monitored='False'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='lirios'
_monitored_repo='liri-vibe'
# END ANTBS METADATA


prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake ../"${_gitname}-${pkgver}" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_INSTALL_LIBDIR=lib \
		-DKDE_INSTALL_LIBEXECDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}

# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=liri-settings
pkgver=0.9.0
pkgrel=1
pkgdesc='Settings application and modules for Liri OS'
arch=('x86_64')
url='https://github.com/lirios/settings'
license=('GPL3')
depends=('vibe' 'greenisland' 'libqtxdg' 'qt5-accountsservice' 'polkit-qt5' 'xkeyboard-config')
makedepends=('extra-cmake-modules')
conflicts=('liri-settings-git')
groups=('liri')
_gitname="${pkgname/liri-}"
source=("https://github.com/lirios/${_gitname}/archive/v${pkgver}.tar.gz")
md5sums=('6e449fa60c2e05c3048479c74cdb6709')


# BEGIN ANTBS METADATA
_is_monitored='False'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='lirios'
_monitored_repo='liri-settings'
# END ANTBS METADATA


prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake ../"${_gitname}-${pkgver}" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_INSTALL_LIBDIR=lib \
		-DKDE_INSTALL_LIBEXECDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}

# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Guillaume Benoit <guillaume@manjaro.org>

pkgname=(pamac pamac-tray-appindicator)
pkgver=7.3.5
_pkgver=7.3.5
pkgrel=1
pkgdesc='A GTK3 frontend for libalpm'
arch=('x86_64')
url='https://gitlab.manjaro.org/applications/pamac'
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3>=0.38' 'gtk3>=3.22'
         'libnotify' 'desktop-file-utils' 'pacman>=5.1' 'pacman<5.2' 'gnutls>=3.4'
         'appstream-glib' 'archlinux-appstream-data' 'git')
makedepends=('gettext' 'meson' 'vala>=0.36.6' 'libappindicator-gtk3' 'gobject-introspection')
backup=('etc/pamac.conf')
conflicts=('pamac-dev')
source=(
	"pamac-$_pkgver.tar.gz::$url/-/archive/v$_pkgver/$pkgname-v$_pkgver.tar.gz"
)
md5sums=('889b99e5e9c904f68e23afac08af8369')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='gitlab'
_monitored_type='releases'
_monitored_project='applications/pamac'
_monitored_repo='pamac'
# END ANTBS METADATA


prepare() {
	cd "${srcdir}/pamac-v${pkgver}"

	# adjust version string
	sed -i -e "s|\"$_pkgver\"|\"$pkgver-$pkgrel\"|g" src/version.vala

	# patches here
}

build() {
	cd "${srcdir}/pamac-v${pkgver}"
	mkdir -p build && cd build

	meson --prefix=/usr --sysconfdir=/etc --libdir=lib -Denable-appindicator=true ..
	ninja
}

package_pamac() {
	optdepends=(
		'pamac-dev-tray-appindicator: tray icon for KDE'
	)
	conflicts=('pamac')
	provides=('pamac')
	backup=('etc/pamac.conf')
	install='pamac.install'


	cd "${srcdir}/pamac-v${pkgver}/build"
	DESTDIR="${pkgdir}" ninja install

	# Remove pamac-tray-appindicator
	rm "${pkgdir}/usr/bin/pamac-tray-appindicator"
	rm -f "${pkgdir}/etc/xdg/autostart/pamac-tray-appindicator.desktop"
}

package_pamac-tray-appindicator() {
	pkgdesc='Tray icon using appindicator for improved KDE support.'
	depends=('pamac' 'libappindicator-gtk3')
	conflicts=('pamac-tray-appindicator-dev')
	backup=()

	cd "${srcdir}/pamac-v${pkgver}/build"
	install -Dm755 src/pamac-tray-appindicator "${pkgdir}/usr/bin/pamac-tray-appindicator"
	install -Dm644 ../data/applications/pamac-tray-appindicator.desktop "${pkgdir}/etc/xdg/autostart/pamac-tray-appindicator.desktop"
}


# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Guillaume Benoit <guillaume@manjaro.org>

pkgname=pamac
pkgver=4.3.5
_pkgver=4.3.5
pkgrel=1
# This is the release package so the below _gitcommit variable should (usually) be commented out.
_gitcommit=""
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/pamac"
license=('GPL3')
depends=('glib2>=2.46' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3>=0.38'
		'gtk3>=3.18' 'libnotify' 'desktop-file-utils' 'pacman' 'gnutls>=3.4')
makedepends=('gettext' 'itstool' 'vala>=0.28')
backup=('etc/pamac.conf')
replaces=('pamac-aur')
conflicts=('pamac-dev')
options=(!emptydirs)


if [ "${_gitcommit}" != "" ]; then
	source=("pamac-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz")
else
	source=("pamac-$_pkgver.tar.gz::$url/archive/v$_pkgver.tar.gz")
fi

md5sums=('3090c9b61478602513914fc3b9eda66c')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='manjaro'
_monitored_repo='pamac'
# END ANTBS METADATA


prepare() {
	if [ "$_gitcommit" != "" ]; then
		mv "$srcdir/pamac-$_gitcommit" "$srcdir/pamac-$pkgver"
	fi

	cd "$srcdir/pamac-$pkgver"

	# adjust version string
	sed -i -e "s|\"$_pkgver\"|\"$pkgver-$pkgrel\"|g" src/manager_window.vala 
	# don't install systemd timers for manjaro's pacman-mirrors script
	sed -i '/pamac-mirrorlist/d' src/Makefile

	# patches here

}

build() {
  cd "$srcdir/pamac-$_pkgver"

  # build
  make all
}

package() {
  cd "$srcdir/pamac-$_pkgver"
  make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install
}



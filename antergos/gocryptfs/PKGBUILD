# Maintainer: Antergos devs <devs@antergos.com>
# AUR Maintainer: Peter Reschenhofer <peter.reschenhofer@gmail.com>
pkgname=gocryptfs
pkgver=1.7
pkgrel=3
pkgdesc="Encrypted overlay filesystem written in Go."
arch=('x86_64')
url="https://github.com/rfjakob/gocryptfs"
license=('MIT')
depends=('gcc-libs' 'openssl' 'fuse')
makedepends=('git' 'go')
source=("git+https://github.com/rfjakob/gocryptfs.git#tag=v${pkgver}"
        "gocryptfs.1")
md5sums=('SKIP'
         '80865b3cefd23da292251e6804388946')

# ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='rfjakob'
_monitored_repo='gocryptfs'
# END ANTBS METADATA

prepare() {
    export GOPATH="$PWD/GO"
    mkdir -p $GOPATH/src/github.com/rfjakob
    ln -sf $PWD/${pkgname} $GOPATH/src/github.com/rfjakob/${pkgname}
    go get -d github.com/rfjakob/${pkgname}
}

build() {
    export GOPATH="$PWD/GO"
    go get -d github.com/hanwen/go-fuse/fuse
    $GOPATH/src/github.com/rfjakob/${pkgname}/build.bash
}

package() {
    install -Dm755 "$PWD/GO/bin/gocryptfs" "$pkgdir/usr/bin/gocryptfs"
    install -Dm644 "gocryptfs.1" "$pkgdir/usr/share/man/man1/gocryptfs.1"
}

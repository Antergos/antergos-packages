# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Jason Scurtu (scujas) <jscurtu@gmail.com>
# Contributor: Marcin Tydelski <marcin.tydelski@gmail.com> 
# Contributor: Jan Lukas Gernert (JeanLuc) <https://launchpad.net/~eviltwin1>

pkgname=feedreader
_pkgname=FeedReader
pkgver=2.0.2
pkgrel=3
pkgdesc='A simple feedreader desktop client for web services like Tiny Tiny RSS.'
arch=('i686' 'x86_64')
url='https://github.com/jangernert/FeedReader'
license=('GPL3')
depends=('sqlite3' 'libnotify' 'html2text' 'libsoup' 'gnome-online-accounts' 'libgee' 'libpeas')
makedepends=('vala' 'gobject-introspection' 'cmake')
groups=('rss')
source=("https://github.com/jangernert/FeedReader/archive/v${pkgver}.tar.gz")
md5sums=('a6467092ec7abb203c351d9e40f5596e')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='jangernert'
_monitored_repo='FeedReader'
# END ANTBS METADATA

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	# Don't update icon cache
	sed -i 's|install (CODE|#|g' CMakeLists.txt
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	[[ -d build ]] && rm -rf build

	mkdir build && cd build

	cmake \
		-DCMAKE_BUILD_TYPE='Release' \
		-DCMAKE_INSTALL_PREFIX='/usr' \
		-DCMAKE_INSTALL_LIBDIR='/usr/lib' \
		-DGSETTINGS_COMPILE='FALSE' \
		..
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}/build"
	make DESTDIR="${pkgdir}" install
}



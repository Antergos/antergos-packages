# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Christian Hesse <mail@eworm.de>

pkgname=opera-developer
pkgver=46.0.2590.0
pkgrel=1
pkgdesc='A fast and secure web browser and Internet suite - developer stream'
arch=('i686' 'x86_64')
url='http://www.opera.com/browser/'
license=('custom:opera')
depends=('alsa-lib' 'nss' 'gtk2' 'gconf' 'libxss' 'libxtst' 'desktop-file-utils')
optdepends=(
	'ffmpeg: HTML5 H264 and mp3 playback'
	'curl: opera crash reporter and autoupdate checker'
)
groups=('browser')
options=(!strip)
source_x86_64=("http://get.geo.opera.com/pub/${pkgname}/${pkgver}/linux/${pkgname}_${pkgver}_amd64.deb")
source_i686=("http://get.geo.opera.com/pub/${pkgname}/${pkgver}/linux/${pkgname}_${pkgver}_i386.deb")
md5sums_i686=('f141cf86c3e1a78eeb471756f94358f9')
md5sums_x86_64=('45b4acda464ebc4f701dd9b3b3337d84')


package() {
	tar -xf data.tar.xz --exclude=usr/share/{lintian,menu} -C "$pkgdir/"
	
	# license
	install -Dm644 \
		"$pkgdir/usr/share/doc/$pkgname/copyright" \
		"$pkgdir/usr/share/licenses/$pkgname/copyright"
	
	cd ${pkgdir}/usr/lib/*-linux-gnu
	cp -R ${pkgname} ${pkgdir}/usr/lib
	
	# set suid bit for Opera sandbox
	chmod 4755 "${pkgdir}/usr/lib/${pkgname}/opera_sandbox"
	
	unlink ${pkgdir}/usr/bin/${pkgname}
	cd ${pkgdir}/usr/bin
	ln -sf /usr/lib/${pkgname}/${pkgname}
}



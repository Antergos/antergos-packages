# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Christian Hesse <mail@eworm.de>

pkgname=opera-developer
pkgver=51.0.2796.0
pkgrel=2
pkgdesc='A fast and secure web browser and Internet suite - developer stream'
arch=('x86_64')
url='http://www.opera.com/browser/'
license=('custom:opera')
depends=('alsa-lib' 'nss' 'gtk2' 'gconf' 'libxss' 'libxtst' 'desktop-file-utils')
optdepends=('ffmpeg: HTML5 H264 and mp3 playback')
groups=('browser')
options=(!strip)
source=("http://ftp.opera.com/pub/${pkgname}/${pkgver}/linux/${pkgname}_${pkgver}_amd64.deb")
md5sums=('b9cc878cf1d97b3aaeb8edd433fbc8a1')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='http'
_monitored_type='file'
_monitored_file_url='https://blogs.opera.com/desktop/changelog-for-51'
_monitored_version_url='https://blogs.opera.com/desktop/changelog-for-51'
_monitored_version_pattern='/(\d\d\.\d\.\d{4}\.\d+)(?=\s*((&#8211;)|(â€“))\s*\d{4}-\d{2}-\d{2})/'
# END ANTBS METADATA


package() {
	tar -xf data.tar.xz --exclude=usr/share/{lintian,menu} -C "$pkgdir/"
	
	# license
	install -Dm644 \
		"$pkgdir/usr/share/doc/$pkgname/copyright" \
		"$pkgdir/usr/share/licenses/$pkgname/copyright"
	
	cd ${pkgdir}/usr/lib/*-linux-gnu
	cp -R ${pkgname} ${pkgdir}/usr/lib
	
	# set suid bit for Opera sandbox
	chmod 4755 "${pkgdir}/usr/lib/${pkgname}/opera_sandbox"
	
	unlink ${pkgdir}/usr/bin/${pkgname}
	cd ${pkgdir}/usr/bin
	ln -sf /usr/lib/${pkgname}/${pkgname}
}



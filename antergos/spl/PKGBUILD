# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=spl
_pkgver=0.7.13
pkgver=0.7.13
pkgrel=3
pkgdesc="Solaris Porting Layer kernel modules."
groups=("system" "zfs")
license=('GPL')
makedepends=("git")
depends=("spl-utils>=${pkgver%+*}" "dkms" "linux-headers" "linux<5.1" "lsb-release")
provides=("spl-dkms=${pkgver}")
conflicts=("spl-dkms")
install=spl.install
arch=("x86_64")
url="http://zfsonlinux.org/"
_commit=''
_tag="spl-${_pkgver}"
source=(
	"https://github.com/zfsonlinux/${pkgname}/archive/${_tag}.zip"
)
md5sums=('d33ecece67d08ea7c0b2103458aaad60')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='zfsonlinux'
_monitored_repo='spl'
_monitored_match_pattern='/(?<=zfs-|spl-)\S+/'
# END ANTBS METADATA

prepare() {
    cd "${srcdir}/${pkgname}-${_tag}"
}

build() {
    cd "${srcdir}/${pkgname}-${_tag}"
    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib \
        --sbindir=/usr/bin \
        --with-config=user
    make
}

package() {
    dkmsdir="${pkgdir}/usr/src/spl-${_pkgver}"
    install -d "${dkmsdir}"

    cp -a "${srcdir}/${pkgname}-${_tag}"/. ${dkmsdir}

    cd "${dkmsdir}"
    ./autogen.sh
    scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n ${pkgname}
    chmod g-w,o-w -R .
}

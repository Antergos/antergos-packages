# Maintainer: Antergos Developers <dev@antergos.com>

pkgname=octopi
pkgdesc="A powerful Qt5 Pacman frontend."
pkgver=0.8.1
pkgrel=2
# This is the release package so the below _gitcommit variable should (usually) be commented out.
#_gitcommit="master"
arch=('i686' 'x86_64')
url="https://github.com/aarnt/octopi"
license=('GPL2')
install="octopi.install"
makedepends=('qt5-declarative' 'qt4' 'knotifications' 'libnotify')
depends=('qt5-base' 'qt5-declarative' 'xterm' 'gksu')
optdepends=('kdesu: for KDE'
            'xfce4-notifyd: for notifications in XFCE'
            'gist: for SysInfo report'
            'pacaur: for AUR support'
            'yaourt: for AUR support')
conflicts=('oktopi-git' 'octopi-notifier-qt4' 'octopi-cachecleaner' 'octopi-repoeditor' 'octopi-notifier')
replaces=('octopi-notifier-qt4' 'octopi-cachecleaner' 'octopi-repoeditor' 'octopi-notifier')
if [ "${_gitcommit}" != "" ]; then
  source=("octopi-${_gitcommit}.tar.gz::https://github.com/aarnt/octopi/archive/${_gitcommit}.tar.gz")
else
  source=("octopi-${pkgver}.tar.gz::https://github.com/aarnt/octopi/archive/v${pkgver}.tar.gz")
fi
source+=('octopi-repoeditor.desktop')
sha256sums=('e01c6d959e5663797771b95b47951e3ad11a9abbc7cbb009f1f835404c5ff43e'
            'd2b2aaf515b096dd75dfe62e45e1d16c0d365d7342960cc36e4d19a0ee2f3e00')

            
prepare() {

if [[ ${_gitcommit} != "" ]]; then
    _SRC_DIR="${srcdir}"/"${pkgname}-${_gitcommit}"
else
    _SRC_DIR="${srcdir}"/"${pkgname}-${pkgver}"
fi

  cd "${_SRC_DIR}"

  # sed version
  sed -i -e "s|0.8.1|${pkgver}-${pkgrel}|g" src/strconstants.h
}

build() {

if [[ ${_gitcommit} != "" ]]; then
    _SRC_DIR="${srcdir}"/"${pkgname}-${_gitcommit}"
else
    _SRC_DIR="${srcdir}"/"${pkgname}-${pkgver}"
fi

  cd "${_SRC_DIR}"

  qmake-qt5 octopi.pro
  make

  cd "${_SRC_DIR}/notifier/pacmanhelper"
  msg "Building pacmanhelper..."
  qmake-qt5 pacmanhelper.pro
  make

  cd "${_SRC_DIR}/notifier/octopi-notifier"
  msg "Building octopi-notifier..."
  qmake-qt5 octopi-notifier.pro
  make

  cd "${_SRC_DIR}/repoeditor"
  msg "Building octopi-repoeditor..."
  qmake-qt5 octopi-repoeditor.pro
  make

  cd "${_SRC_DIR}/cachecleaner"
  msg "Building octopi-cachecleaner..."
  qmake-qt5 octopi-cachecleaner.pro
  make
}

package() {

if [[ ${_gitcommit} != "" ]]; then
    _SRC_DIR="${srcdir}"/"${pkgname}-${_gitcommit}"
else
    _SRC_DIR="${srcdir}"/"${pkgname}-${pkgver}"
fi
  
  cd "${_SRC_DIR}"
 
	install -D -m755 "${_SRC_DIR}/bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -D -m644 "${_SRC_DIR}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	install -D -m644 "${_SRC_DIR}/resources/images/${pkgname}_green.png" "${pkgdir}/usr/share/icons/${pkgname}.png"
	install -D -m644 "${_SRC_DIR}/resources/images/${pkgname}_green.png" "${pkgdir}/usr/share/icons/gnome/32x32/apps/${pkgname}.png"
	install -D -m644 "${_SRC_DIR}/resources/images/${pkgname}_red.png" "${pkgdir}/usr/share/icons/${pkgname}_red.png"
	install -D -m644 "${_SRC_DIR}/resources/images/${pkgname}_yellow.png" "${pkgdir}/usr/share/icons/${pkgname}_yellow.png"
  
  #Pacmanhelper service files
  install -D -m755 "${_SRC_DIR}/notifier/bin/pacmanhelper" "${pkgdir}/usr/lib/octopi/pacmanhelper"

	#speedup files
	install -D -m755 "${_SRC_DIR}/speedup/speedup-octopi.sh" "${pkgdir}/usr/bin/speedup-octopi.sh"
	install -D -m644 "${_SRC_DIR}/speedup/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

  install -D -m644 "${_SRC_DIR}/notifier/pacmanhelper/polkit/org.octopi.pacman.policy" "${pkgdir}/usr/share/polkit-1/actions/org.octopi.pacman.policy"
  install -D -m644 "${_SRC_DIR}/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.conf" "${pkgdir}/etc/dbus-1/system.d/org.octopi.pacmanhelper.conf"
  install -D -m644 "${_SRC_DIR}/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.xml" "${pkgdir}/usr/share/dbus-1/interfaces/org.octopi.pacmanhelper.xml"
  install -D -m644 "${_SRC_DIR}/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.service" "${pkgdir}/usr/share/dbus-1/system-services/org.octopi.pacmanhelper.service"
  
  #Octopi-notifier file
  install -D -m755 "${_SRC_DIR}/notifier/bin/octopi-notifier" "${pkgdir}/usr/bin/octopi-notifier"
  install -D -m644 "${_SRC_DIR}/octopi-notifier.desktop" "${pkgdir}/usr/share/applications/octopi-notifier.desktop"
  install -D -m644 "${_SRC_DIR}/octopi-notifier.desktop" "${pkgdir}/etc/xdg/autostart/octopi-notifier.desktop"
  
  #Octopi-repoeditor
  install -D -m755 "${_SRC_DIR}/repoeditor/bin/octopi-repoeditor" "${pkgdir}/usr/bin/octopi-repoeditor"
  install -D -m644 "${srcdir}/${pkgname}-repoeditor.desktop" "${pkgdir}/usr/share/applications/${pkgname}-repoeditor.desktop"
	install -D -m644 "${_SRC_DIR}/resources/images/${pkgname}_red.png" "${pkgdir}/usr/share/icons/${pkgname}.png"
	
	#Octopi-cachecleaner file
  install -D -m755 "${_SRC_DIR}/cachecleaner/bin/octopi-cachecleaner" "${pkgdir}/usr/bin/octopi-cachecleaner"
  install -D -m644 "${_SRC_DIR}/cachecleaner/octopi-cachecleaner.desktop" "${pkgdir}/usr/share/applications/octopi-cachecleaner.desktop"
}


# -*- mode: bash; tab-width: 4; indent-tabs-mode: t -*-

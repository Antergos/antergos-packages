# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=dolphin-root
_pkgname=dolphin
pkgver=18.08.0
pkgrel=1
pkgdesc="KDE File Manager"
arch=('x86_64')
url="https://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=('baloo-widgets' 'knewstuff' 'kio-extras' 'kcmutils' 'kparts' 'kinit')
makedepends=('extra-cmake-modules' 'kdoctools')
optdepends=('kde-cli-tools: for editing file type options'
    'ffmpegthumbs: video thumbnails'
    'ruby: installing new service menus from KDE Store'
    'kdegraphics-thumbnailers: PDF and PS thumbnails'
    'konsole: terminal panel'
    'purpose: share context menu')
conflicts=('kdebase-dolphin<15.08' 'dolphin')
provides=('kdebase-dolphin' 'dolphin')
groups=('kde-applications' 'kdebase')
source=("https://download.kde.org/stable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz"
	dolphin-root.patch)
sha256sums=('5efb4c5dc304a6d179915b6553ad0f5232dc4e6b38a9d4dbd80a72a5351e6fe4'
            'b4969d7367d1304f460862946fa44ce46bc429dac2c48d09a8559d6ab98ca8d2')

prepare() {
    mkdir -p build

    cd ${_pkgname}-${pkgver}/src
    patch -Np1 -i "${srcdir}/dolphin-root.patch"
}

build() {
    cd build
    cmake ../$_pkgname-$pkgver \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DBUILD_TESTING=OFF
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
}

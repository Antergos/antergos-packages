# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=zfs
_pkgver=0.7.0rc3
_rev=2609
_kern=4.9.6_1
pkgver="${_pkgver}.r${_rev}.${_kern}"
pkgrel=1
pkgdesc="Kernel modules for the Zettabyte File System."
license=('CDDL')
depends=("spl=${pkgver/2609/1017}" "zfs-utils=${pkgver}" "dkms" "linux-headers" "linux<4.11")
provides=("zfs-dkms=${pkgver}")
conflicts=("zfs-git" "zfs-dkms")
install=zfs.install
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
_commit='10a3f8885904558c2a406151c4cf960371c34934'
source=("https://github.com/zfsonlinux/${pkgname}/archive/${_commit}.zip")
md5sums=('f91a42244dfc05e17cc97e40d68b91cd')



build() {
	cd "${srcdir}/${pkgname}-${_commit}"
	./autogen.sh

	{ [[ "${CARCH}" == i686 ]] && _i686_target="--target=i686-linux-gnu"; } || _i686_target=""

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--libdir=/usr/lib \
		--datadir=/usr/share \
		--includedir=/usr/include \
		--with-udevdir=/usr/lib/udev \
		--libexecdir=/usr/lib/zfs \
		--with-config=user \
		${_i686_target}
	make
}

package() {
	dkmsdir="${pkgdir}/usr/src/zfs-${_pkgver}"
	install -d "${dkmsdir}"

	cp -a "${srcdir}/${pkgname}-${_commit}"/. ${dkmsdir}

	cd "${dkmsdir}"
	./autogen.sh
	scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n zfs
	chmod g-w,o-w -R .
}



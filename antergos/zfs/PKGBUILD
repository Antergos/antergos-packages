# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: isiachi <isiachi@rhyeworld.it>

pkgname=zfs
_pkgver=0.7.13
pkgver=0.7.13
pkgrel=3
pkgdesc="Kernel modules for the Zettabyte File System."
license=('CDDL')
groups=("system" "zfs")
makedepends=("git")
depends=("spl>=${pkgver%+*}" "zfs-utils>=${pkgver%+*}" "dkms" "linux-headers" "linux<5.0")
provides=("zfs-dkms=${pkgver}")
conflicts=("zfs-git" "zfs-dkms")
install=zfs.install
arch=("x86_64")
url="http://zfsonlinux.org/"
_commit=''
_tag="zfs-${_pkgver}"
source=(
	"https://github.com/zfsonlinux/${pkgname}/archive/${_tag}.zip"
	'linux420.patch::https://github.com/zfsonlinux/zfs/pull/8227.patch'
)
md5sums=('bb1b4076abac5e17aa9b6759683839dc'
         '6500e4f3abd3a54c96362b057f1a9867')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='zfsonlinux'
_monitored_repo='zfs'
_monitored_match_pattern='/(?<=zfs-|spl-)\S+/'
# END ANTBS METADATA


prepare() {
	cd "${srcdir}/${pkgname}-${_tag}"
	patch -Np1 -i ../linux420.patch
}

build() {
    cd "${srcdir}/${pkgname}-${_tag}"
    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --bindir=/usr/bin \
        --sbindir=/usr/bin \
        --libdir=/usr/lib \
        --datadir=/usr/share \
        --includedir=/usr/include \
        --with-udevdir=/usr/lib/udev \
        --libexecdir=/usr/lib/zfs \
        --with-config=user \
        --with-spl="/usr/src/spl-${_pkgver}"
    make
}

package() {
    dkmsdir="${pkgdir}/usr/src/zfs-${_pkgver}"
    install -d "${dkmsdir}"

    cp -a "${srcdir}/${pkgname}-${_tag}"/. ${dkmsdir}

    cd "${dkmsdir}"
    ./autogen.sh
    scripts/dkms.mkconf -v ${_pkgver} -f dkms.conf -n ${pkgname}
    chmod g-w,o-w -R .
}

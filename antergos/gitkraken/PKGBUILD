# Maintainer: Tim Kleinschmidt <tim.kleinschmidt@gmail.com>
# Contributor: Marcin Wieczorek <marcin@marcin.co>

pkgname=gitkraken
pkgrel=1
pkgver=2.1.0
pkgdesc="The intuitive, fast, and beautiful cross-platform Git client."
url="http://www.gitkraken.com/"
provides=('gitkraken')
arch=('x86_64')
license=('custom')
groups=('development')
depends=('gtk2' 'git' 'nss' 'libnotify' 'libxtst' 'libgnome-keyring' 'gconf' 'alsa-lib')
source=(
	"https://release.gitkraken.com/linux/v${pkgver}.tar.gz"
	"GitKraken.desktop"
	"gitkraken.png"
	"eula.html"
)
md5sums=('8e99c4dfd5b424beb250707476528933'
         'cf2a1746a0cafe11793af0801c78afeb'
         '04987b933d551d15c0813111d715bb9e'
         'e9ba6663e5f1f92cae31beb0074e8c6c')

package() {
	install -d "${pkgdir}"/opt
	cp -R "${srcdir}"/gitkraken "${pkgdir}"/opt/gitkraken

	install -d "${pkgdir}"/usr/bin
	ln -s /opt/gitkraken/gitkraken "${pkgdir}"/usr/bin/gitkraken

	install -D -m644 "./eula.html" "${pkgdir}/usr/share/licenses/${pkgname}/eula.html"
	install -D -m644 "./GitKraken.desktop" "${pkgdir}/usr/share/applications/GitKraken.desktop"
	install -D -m644 "./gitkraken.png" "${pkgdir}/usr/share/pixmaps/gitkraken.png"
}

# Mantainer: Antergos Developers <dev@antergos.com>
# Contrubutor: Phillip Schichtel <phillip.public@schich.tel>

pkgname=gtk-theme-adapta
_theme_name=adapta-gtk-theme
pkgver=3.22.4.1
pkgrel=1
pkgdesc="An adaptive Gtk+ theme based on Material Design Guidelines."
arch=('any')
url="https://github.com/tista500/Adapta"
license=('GPL2')
depends=('gtk2' 'gtk-engine-murrine' 'gtk-engines' 'gtk3>3.18')
makedepends=('glib2' 'ruby-sass' 'ruby-bundler' 'inkscape')
groups=('themes' 'themes::gtk')
source=("https://github.com/adapta-project/${_theme_name}/archive/${pkgver}.tar.gz")
md5sums=('fa79d9be0677aab1b92cab512f5d4aeb')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='adapta-project'
_monitored_repo='adapta-gtk-theme'
_monitored_match_pattern='/(3\.22\.\d+\.\d{1,3})$/'
# END ANTBS METADATA


build() {
	cd "${_theme_name}-${pkgver}"
	./autogen.sh \
		--disable-unity \
		--enable-gtk_next \
		--enable-chrome \
		--enable-plank \
		--with-selection_color=#2196F3 \
		--with-second_selection_color=#64B5F6 \
		--with-accent_color=#F06292 \
		--with-suggestion_color=#E91E63

	make
}

package() {
	cd "${_theme_name}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}

# -*- mode: bash;-*-

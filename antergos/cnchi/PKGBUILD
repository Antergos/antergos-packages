# $Id$
# Maintainer: Antergos Developers <dev@antergos.com>

pkgname=cnchi
_pkgname=Cnchi
pkgver=0.14.240
pkgrel=1
pkgdesc='A modern, flexible system installer for Linux'
arch=('any')
license=('GPL3')
url='https://github.com/Antergos/Cnchi'
depends=('python' 'pyalpm' 'python-gobject' 'python-dbus' 'python-cairo'
        'python-requests' 'python-parted' 'webkit2gtk' 'upower' 'python-mako'
        'iso-codes' 'gptfdisk' 'python-bugsnag')
conflicts=('cnchi-dev')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/antergos/cnchi/archive/${pkgver}.tar.gz")
sha256sums=('693da2b4ef8c50ef4f51d325bb0a7710e9ed2762b1a573cb711d29095622b746')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='antergos'
_monitored_repo='cnchi'
# END ANTBS METADATA

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	install -d ${pkgdir}/usr/share/{cnchi,locale}
	install -Dm755 "bin/${pkgname}" "${pkgdir}/usr/bin/cnchi"
	install -Dm755 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/cnchi.desktop"
    install -Dm644 "data/images/antergos/antergos-icon.png" "${pkgdir}/usr/share/pixmaps/cnchi.png"

	sed -r -i 's|\/usr.+ -v|pkexec /usr/share/cnchi/bin/cnchi -s bugsnag|g' "${pkgdir}/usr/bin/cnchi"

	for i in cnchi bin data scripts ui; do
		cp -R ${i} "${pkgdir}/usr/share/cnchi/"
	done

	for files in po/*; do
		if [ -f "$files" ] && [ "$files" != 'po/cnchi.pot' ]; then
			STRING_PO=`echo ${files#*/}`
			STRING=`echo ${STRING_PO%.po}`
			mkdir -p ${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES
			msgfmt $files -o ${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES/cnchi.mo
		 	echo "${STRING} installed..."
		fi
	done
}

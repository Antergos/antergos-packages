# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=caja
pkgver=1.20.3
pkgrel=1
pkgdesc='File manager for the MATE desktop'
url='https://mate-desktop.org'
arch=('x86_64')
license=('GPL')
depends=('exempi' 'gtk3' 'gvfs' 'libexif' 'python-gobject' 'libxt' 'mate-desktop'
		'startup-notification' 'libnotify')
makedepends=('wget' 'gobject-introspection' 'mate-common' 'intltool')
optdepends=('gstreamer: automatic media playback when mouseover'
			'gvfs-smb: To connect to Samba/Windows shares')
groups=('mate')
source=("https://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz")
_sha1sums_url="https://pub.mate-desktop.org/releases/${pkgver::(-2)}/SHA1SUMS"
sha1sums=($(wget -q "${_sha1sums_url}" && grep -m 1 "${pkgname}-${pkgver}.tar.xz" SHA1SUMS | cut -f1))

# BEGIN ANTBS METADATA
_autosums='True'
_is_monitored='True'
_monitored_service='mate-desktop'
_monitored_match_pattern='/^\d\.\d[02468]\.\d+$/'
# END ANTBS METADATA

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export PATH="/usr/bin:${PATH}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/${pkgname} \
		--enable-introspection \
		--disable-update-mimedb
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}


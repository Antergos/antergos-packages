# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=pluma
pkgver=1.20.4
pkgrel=1
pkgdesc="A powerful text editor for the MATE Desktop"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('iso-codes' 'mate-desktop' 'zenity' 'gtksourceview3' 'libpeas' 'python2-gobject' 'yelp')
makedepends=('intltool' 'itstool' 'gobject-introspection' 'python2' 'mate-common' 'yelp-tools')
groups=('mate-extra')
source=(
	"https://pub.mate-desktop.org/releases/${pkgver::(-2)}/${pkgname}-${pkgver}.tar.xz"
)
_sha1sums_url="https://pub.mate-desktop.org/releases/${pkgver::(-2)}/SHA1SUMS"
sha1sums=(
	$(wget -q "${_sha1sums_url}" && grep "${pkgname}-${pkgver}.tar.xz" SHA1SUMS | cut -f1)
)


# BEGIN ANTBS METADATA
_autosums='True'
_is_monitored='True'
_monitored_service='mate-desktop'
_monitored_match_pattern='/^\d\.\d[02468]\.\d+$/'
# END ANTBS METADATA

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's/python/python2/' plugins/externaltools/data/switch-c.tool.in
	sed -i 's/, enchant/, enchant-2/' configure.ac
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./autogen.sh
	PYTHON=/usr/bin/python2 ./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/${pkgname} \
		--disable-static \
		--enable-gtk-doc-html \
		--enable-gvfs-metadata \
		--disable-schemas-compile \
		--enable-python
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}

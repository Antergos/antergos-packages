# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: ELmoussaoui Bilal <bil.elmoussaoui@gmail.com>

pkgname=arc-icon-theme
pkgver=20161122
pkgrel=1
pkgdesc='Base icon theme from the creator of the popular Arc GTK Theme.'
arch=('any')
url='https://github.com/horst3180/arc-icon-theme'
license=('GPL3')
groups=('themes' 'themes::icon')
depends=('numix-icon-theme-square' 'gtk-update-icon-cache')
makedepends=('automake' 'unzip')
source=("${pkgname}-${pkgver}::https://github.com/horst3180/${pkgname}/archive/${pkgver}.zip")
md5sums=('be62051ff490d4657f4fd9b81bac29f0')
conflicts=('arc-icon-theme-git')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='horst3180'
_monitored_repo='arc-icon-theme'
_monitored_match_pattern='/\d{8}/'
# END ANTBS METADATA

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Inherit from Numix Square (for application and fallback icons).
	sed -i 's|Inherits=Moka|Inherits=Numix-Square,Numix|g' Arc/index.theme

	./autogen.sh --prefix=/usr

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}



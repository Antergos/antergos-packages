# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: TZ86
# Contributor: killajoe

_archgit_url='https://git.archlinux.org/svntogit/packages.git/plain/trunk'

_from_arch_pkg() {
	echo "${1}::${_archgit_url}/${1}?h=packages/chromium"
}

pkgname=vivaldi
pkgver=1.15.1147.64
# https://update.vivaldi.com/update/1.0/relnotes/1.15.1147.64.html
_chromium=68.0.3440.106 # https://omahaproxy.appspot.com
pkgrel=4
pkgdesc='An advanced browser made with the power user in mind.'
groups=('browser')
url="https://vivaldi.com"
options=('!strip' '!zipman')
license=('custom' 'LGPL2.1')
arch=('x86_64')
depends=('gtk3' 'nss' 'libxtst' 'gconf' 'libxss' 'freetype2' 'ttf-font' 'nspr'
		'desktop-file-utils' 'shared-mime-info' 'alsa-lib' 'hicolor-icon-theme')
makedepends=(
	'w3m' 'libexif' 'libpulse' 'libxss' 'ninja' 'pciutils' 'python2' 'xdg-utils' 'nodejs'
	'opus' 'gperf' 'yasm' 'mesa' 'harfbuzz-icu' 'libwebp' 'libxslt' 'libxml2' 're2' 'minizip'
	'snappy' 'clang' 'lld'
)
provides=('vivaldi-ffmpeg-codecs' 'vivaldi-widevine')
conflicts=('vivaldi-ffmpeg-codecs' 'vivaldi-widevine')
replaces=('vivaldi-ffmpeg-codecs' 'vivaldi-widevine')
noextract=('chrome.rpm')

source=(
	"https://downloads.vivaldi.com/stable/vivaldi-stable-${pkgver}-1.x86_64.rpm"
	"https://commondatastorage.googleapis.com/chromium-browser-official/chromium-$_chromium.tar.xz"
	"chrome.rpm::https://dl.google.com/linux/direct/google-chrome-stable_current_x86_64.rpm"
	'chrome-eula_text.html::https://www.google.com/intl/en/chrome/browser/privacy/eula_text.html'
	"$(_from_arch_pkg x11-fix-mixup-between-DIP-pixel-coordinates.patch)"
	"$(_from_arch_pkg blink-disable-XML-catalogs-at-runtime.patch)"
	"$(_from_arch_pkg fix-cfi-icall-failure-with-use_system_libjpeg-true.patch)"
        "$(_from_arch_pkg only-disable-cfi-icall-when-use_system_libjpeg-true.patch)"
	"$(_from_arch_pkg chromium-cors-string-r0.patch)"
	"$(_from_arch_pkg chromium-ffmpeg-r1.patch)"
        "$(_from_arch_pkg chromium-widevine-r2.patch)"
	"$(_from_arch_pkg chromium-libjpeg-r0.patch)"
	"$(_from_arch_pkg chromium-libwebp-shim-r0.patch)"
        "$(_from_arch_pkg chromium-skia-harmony.patch)"

)
md5sums=('7ebefebb5262284f9cea553e650f0534'
         '45f1c943546c008051ec39df51aa20c0'
         '63a59a953b874e70d931807f50f23622'
         'a2bf9d56aff00882b591edc55cbd6676'
         '053ece2216346c5ee090703fd6230402'
         'ce00e976bcef638a96f723487f78892d'
         '7d208ab5982df74e56bfca6bfb3c5c16'
         'adf54dfc2f529b24558b3dcfd79db5f7'
         '9a2749a7dfd460b43666eabf8f28d13a'
         'ab9a977ec1c0d0951093db24d855a3be'
         '56ebefbdfafb2d7ede199a4257d514d4'
         '5fbfcbedbe9bcaca240a675836c81769'
         '1e113cab22b9c247713f57a1af2b744b'
         '254061cb349cff714230b7cca83b0a4c')





# Possible replacements are listed in build/linux/unbundle/replace_gn_files.py
# Keys are the names in the above script; values are the dependencies in Arch
declare -rgA _system_libs=(
  #[ffmpeg]=ffmpeg            # https://crbug.com/731766
  [flac]=flac
  #[fontconfig]=fontconfig    # Enable for M65
  #[freetype]=freetype2       # Using 'use_system_freetype=true' until M65
  #[harfbuzz-ng]=harfbuzz     # Using 'use_system_harfbuzz=true' until M65
  [icu]=icu
  [libdrm]=''
  [libjpeg]=libjpeg
  #[libpng]=libpng            # https://crbug.com/752403#c10
  #[libvpx]=libvpx            # https://bugs.gentoo.org/611394
  [libwebp]=libwebp
  #[libxml]=libxml2           # https://crbug.com/736026
  [libxslt]=libxslt
  [opus]=opus
  [re2]=re2
  [snappy]=snappy
  [yasm]=''
  [zlib]=minizip
)
declare -rga _unwanted_bundled_libs=(
  ${!_system_libs[@]}
  ${_system_libs[libjpeg]+libjpeg_turbo}
  freetype
  harfbuzz-ng
)
depends+=(${_system_libs[@]} freetype2 harfbuzz)

prepare() {
	cd "$srcdir/chromium-$_chromium"

	# https://crbug.com/710701
	local _chrome_build_hash=$(
		curl -s https://chromium.googlesource.com/chromium/src.git/+/$_chromium?format=TEXT | base64 -d | grep -Po '^parent \K[0-9a-f]{40}$'
	)

	if [[ -z $_chrome_build_hash ]]; then
		error "Unable to fetch Chrome build hash."
		return 1
	fi

	echo "LASTCHANGE=$_chrome_build_hash-" >build/util/LASTCHANGE

  # Work around broken screen sharing in Google Meet
  # https://crbug.com/829916#c16
  sed -i 's/"Chromium/"Chrome/' chrome/common/chrome_content_client_constants.cc

  # https://crbug.com/707721
  patch -Np1 -i ../x11-fix-mixup-between-DIP-pixel-coordinates.patch

  # https://crbug.com/736026
  patch -Np1 -i ../blink-disable-XML-catalogs-at-runtime.patch

  # https://crbug.com/866290
  patch -Np1 -i ../fix-cfi-icall-failure-with-use_system_libjpeg-true.patch
  patch -Np1 -i ../only-disable-cfi-icall-when-use_system_libjpeg-true.patch

  # https://crbug.com/skia/6663#c10
  patch -Np4 -i ../chromium-skia-harmony.patch

  # Fixes from Gentoo
  patch -Np1 -i ../chromium-cors-string-r0.patch
  patch -Np1 -i ../chromium-ffmpeg-r1.patch
  patch -Np1 -i ../chromium-libjpeg-r0.patch
  patch -Np1 -i ../chromium-libwebp-shim-r0.patch
  patch -Np1 -i ../chromium-widevine-r2.patch
	
	# Remove compiler flags not supported by our system clang
	sed -i \
		-e '/"-Wno-enum-compare-switch"/d' \
		-e '/"-Wno-null-pointer-arithmetic"/d' \
		-e '/"-Wno-tautological-unsigned-zero-compare"/d' \
		-e '/"-Wno-tautological-constant-compare"/d' build/config/compiler/BUILD.gn

	# chromium 46 gives an error about a missing file
	# workaround create empty
	touch chrome/test/data/webui/i18n_process_css_test.html

	# Use Python 2
	find . -name '*.py' -exec sed -i -r 's|/usr/bin/python$|&2|g' {} +

	# There are still a lot of relative calls which need a workaround
	[[ -d "$srcdir/python2-path" ]] && rm -rf "$srcdir/python2-path"
	mkdir "$srcdir/python2-path"
	ln -s /usr/bin/python2 "$srcdir/python2-path/python"

	mkdir -p third_party/node/linux/node-linux-x64/bin
	ln -s /usr/bin/node third_party/node/linux/node-linux-x64/bin/

	# Remove bundled libraries for which we will use the system copies; this
	# *should* do what the remove_bundled_libraries.py script does, with the
	# added benefit of not having to list all the remaining libraries
	local _lib
	for _lib in "${_unwanted_bundled_libs[@]}"
	do
		find -type f -path "*third_party/$_lib/*" \
			\! -path "*third_party/$_lib/chromium/*" \
			\! -path "*third_party/$_lib/google/*" \
			\! -path "*base/third_party/icu/*" \
			\! -regex '.*\.\(gn\|gni\|isolate\|py\)' \
			-delete
	done

	python2 build/linux/unbundle/replace_gn_files.py --system-libraries "${!_system_libs[@]}"
}

build() {
	cd "$srcdir/chromium-$_chromium"

	export PATH="$srcdir/python2-path:$PATH"
	export HOME="$srcdir"
	export TMPDIR="$srcdir/temp"
	mkdir -p "$TMPDIR"

	export CC=clang
	export CXX=clang++
	export AR=ar
	export NM=nm

	local args=(
		'clang_use_chrome_plugins=false'
		'fieldtrial_testing_like_official_build=true'
		'remove_webcore_debug_symbols=true'
		'ffmpeg_branding="Chrome"'
		'proprietary_codecs=true'
		'enable_hevc_demuxing=true'
		'link_pulseaudio=true'
		'use_gconf=false'
		'use_gio=false'
		'use_gnome_keyring=false'
		'use_kerberos=false'
		'use_cups=false'
		'use_sysroot=false'
		'use_gold=false'
		'linux_use_bundled_binutils=false'
		'use_gtk3=true'
		'use_custom_libcxx=false'
		'fatal_linker_warnings=false'
		'treat_warnings_as_errors=false'
		'is_clang=true'
		'custom_toolchain="//build/toolchain/linux/unbundle:default"'
		'host_toolchain="//build/toolchain/linux/unbundle:default"'
		'is_component_build=true'
		'is_debug=false'
		'symbol_level=0'
		'enable_nacl=false'
		'enable_swiftshader=false'
		'enable_nacl=false'
		'enable_widevine=true'
		'use_system_freetype=true'
		'use_system_harfbuzz=true'
	)

	python2 tools/gn/bootstrap/bootstrap.py --gn-gen-args "${args[*]}"
	out/Release/gn gen out/Release --args="${args[*]}" --script-executable=/usr/bin/python2

	ninja -C out/Release -v media/ffmpeg
}


package() {
	cp -a {opt,usr} "${pkgdir}"

	# File conflicts with exo package
	rm "${pkgdir}/usr/share/xfce4/helpers/vivaldi.desktop"

	# suid sanbox
	chmod 4755 "${pkgdir}/opt/vivaldi/vivaldi-sandbox"

	# make /usr/bin/vivaldi available if its not there
	if [[ ! -e "$pkgdir/usr/bin/vivaldi-stable" ]]; then
		install -dm 755 "$pkgdir/usr/bin"
		ln -sf /opt/vivaldi/vivaldi "$pkgdir/usr/bin/vivaldi-stable"
	fi

	# install icons
	for res in 16 22 24 32 48 64 128 256; do
		install -Dm644 "${pkgdir}/opt/vivaldi/product_logo_${res}.png" \
		"${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vivaldi.png"
	done

	# license
	install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
	strings "$pkgdir/opt/vivaldi/locales/en-US.pak" \
		| tr '\n' ' ' \
		| sed -rne 's/.*(<html lang.*>.*html>).*/\1/p' \
		| w3m -I 'utf-8' -T 'text/html' \
		> "$pkgdir/usr/share/licenses/$pkgname/eula.txt"

	# Install ffmpeg codec
	(cd "$srcdir/chromium-$_chromium" \
		&& install -Dm644 out/Release/libffmpeg.so "$pkgdir/opt/vivaldi/libffmpeg.so")

	# Extract and install widevine codec
	mkdir "${srcdir}/chrome" && cd "${srcdir}/chrome"
	bsdtar -xf ../chrome.rpm opt/google/chrome/libwidevinecdm.so

	# Copy to Vivaldi directory for now. We might add a symlink when package is installed (using alpm hook).
	unlink "${pkgdir}/opt/vivaldi/libwidevinecdm.so" || true
	cp opt/google/chrome/libwidevinecdm.so "${pkgdir}/opt/vivaldi"
	
	# Chrome license
	cp "${srcdir}/chrome-eula_text.html" "${pkgdir}/usr/share/licenses/${pkgname}"

	# ALPM Hooks
	mkdir -p "${pkgdir}/usr/share/libalpm"/{hooks,scripts}
	(cd "${srcdir}" \
		&& cp ../*.hook "${pkgdir}/usr/share/libalpm/hooks" \
		&& install -Dm755 ../vivaldi-widevine-helper "${pkgdir}/usr/share/libalpm/scripts/vivaldi-widevine-helper")
}

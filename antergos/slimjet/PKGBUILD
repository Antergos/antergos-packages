# Maintainer: Antergos Developers <dev@antergos.com>
# Contrubutor: eagleeyetom <eagleeyetom@gmail.com>
# Contrubutor: raininja <raijin@rai.ninja>

pkgname=slimjet
pkgver=20.0.5.0
pkgrel=1
pkgdesc="Fast, smart and powerful browser based on Blink"
arch=('x86_64')
url="http://www.slimjet.com"
license=('custom:slimjet')
depends=('alsa-lib' 'desktop-file-utils' 'flac' 'gconf' 'gtk2' 'harfbuzz' 'harfbuzz-icu'
		'hicolor-icon-theme' 'icu' 'libpng' 'libxss' 'libxtst' 'nss' 'openssl' 'nspr' 'opus'
		'snappy' 'speech-dispatcher' 'ttf-font' 'xdg-utils')
optdepends=('kdebase-kdialog: needed for file dialogs in KDE'
			'ttf-liberation: fix fonts for some PDFs')
provides=('slimjet')
options=('!emptydirs' '!strip')
groups=('browser')
source_x86_64=("${pkgname}_amd64.deb::http://www.slimjet.com/release/${pkgname}_amd64.deb")
md5sums_x86_64=('ee562e8460ba154a81cfa3405a594198')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='http'
_monitored_type='file'
_monitored_file_url='http://www.slimjetbrowser.com/release/slimjet_amd64.deb'
_monitored_version_url='http://www.slimjet.com/en/dlpage.php'
_monitored_version_pattern='/Stable Version\s*\(\s*([\d\.]+)\s*\)/'
# END ANTBS METADATA


package() {
    msg2 "Extracting the data tarball..."
    bsdtar -xf data.tar.xz -C "$pkgdir/"

    msg2 "Correcting permissions..."
    chmod -R 0755 "$pkgdir/"

    _crypto_files="libnspr4.so.0d libplds4.so.0d libplc4.so.0d libssl3.so.1d libnss3.so.1d libsmime3.so.1d libnssutil3.so.1d"
    _libdir="/usr/lib"

    add_crypto_symlinks() {

      for f in $_crypto_files; do
        target=$(echo $f | sed 's/.[01]d$//')
        if [ -f "/$_libdir/$target" ]; then
            ln -snf "/$_libdir/$target" "$pkgdir/opt/slimjet/$f"
            else echo "CHECK NSS, OPENSSL, AND NSPR VERSIONS"
            exit 1
        fi
        done
    }

    add_license_note() {
    mkdir "$pkgdir/usr/share/licenses/"
    install  "../LICENSE" "$pkgdir/usr/share/licenses/${pkgname}"
    }

    # Arch has  libudev.so.1, slimjet wants .0
    _libudev_0=libudev.so.0
    _libudev_1=libudev.so.1

    add_udev_symlinks() {
    ln -snf "/$_libdir/$_libudev_1" "$pkgdir/opt/slimjet/$_libudev_0"
    }

    msg2 "Adding udev and crypto symlinks, and a LICENSE ..."
    add_udev_symlinks
    add_crypto_symlinks
    add_license_note

    msg2 "Adding Icons..."
    for i in 16x16 22x22 24x24 32x32 48x48 64x64 128x128 256x256; do
        install -Dm644 "$pkgdir"/opt/slimjet/product_logo_${i/x*}.png \
                       "$pkgdir"/usr/share/icons/hicolor/$i/apps/flashpeak-slimjet.png
    done
}

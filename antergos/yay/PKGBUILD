# Maintainer: Antergos devs <devs@antergos.com>
# AUR Maintainer: Jguer <joaogg3@gmail.com>
pkgname=yay
pkgver=9.2.0
pkgrel=1
pkgdesc="Yet another yogurt. Pacman wrapper and AUR helper written in go."
arch=('x86_64')
url="https://github.com/Jguer/yay"
license=('GPL')
depends=('pacman>=5.1' 'sudo' 'git')
makedepends=('go')
conflicts=('yay-bin' 'yay-git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Jguer/${pkgname}/archive/v${pkgver}.tar.gz")
sha1sums=('0318e1f6437dab7c71a795cbb12d8004ecc508a5')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='Jguer'
_monitored_repo='yay'
# END ANTBS METADATA

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make VERSION=$pkgver DESTDIR="$pkgdir" PREFIX=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make VERSION=$pkgver DESTDIR="$pkgdir" PREFIX=/usr install
}

# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Richard Jackson <rj@iinet.net.au>

pkgname=abiword-gtk2
_pkgname=abiword
pkgver=3.0.2
pkgrel=1
pkgdesc='Fully-featured word processor, Gtk2, With plugins, Lite version'
arch=('x86_64')
license=('GPL')
depends=('fribidi' 'wv' 'librsvg' 'enchant' 'desktop-file-utils' 'gtk2' 'libxslt')
makedepends=('pkgconfig' 'boost')
conflicts=('abiword' 'abiword-plugins')
url='http://www.abisource.com'
source=("$_pkgname-$pkgver.tar.gz::http://abisource.com/downloads/$_pkgname/$pkgver/source/$_pkgname-$pkgver.tar.gz")
md5sums=('cda6dd58c747c133b421cc7eb18f5796')

build() {
	cd $_pkgname-$pkgver

	./configure --prefix=/usr \
		--enable-shared \
		--disable-static \
		--with-gtk2 \
		--enable-clipart \
		--enable-templates \
		--without-redland \
		--without-libical \
		--disable-collab-backend-service \
		--disable-collab-backend-tcp

	make -C goffice-bits2
	make
}


package() {
	cd $_pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}

# -*- mode: bash;-*

# Maintainer: Antergos Developers <dev@antergos.com>


pkgname=tldr-python-client
pkgver='0.3'
pkgrel=2
pkgdesc='Command line client for tldr, a collection of simplified and community-driven man pages.'
url='https://github.com/tldr-pages/tldr-python-client'
provides=('tldr-client')
conflicts=('tldr-client')
depends=('python')
makedepends=('python-setuptools')
license=('MIT')
arch=('any')
source=("git+https://github.com/tldr-pages/${pkgname}.git#tag=${pkgver}"
		'https://github.com/tldr-pages/tldr-python-client/pull/32.patch')
md5sums=('SKIP'
		'617fa4da21aefb6b5688d97d5fb12955')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='tldr-pages'
_monitored_repo='tldr-python-client'
# END ANTBS METADATA


build() {
	cd "${srcdir}/${pkgname}"

	# https://github.com/tldr-pages/tldr-python-client/pull/32
	patch -Np1 -i ../32.patch

	python setup.py build
}


package() {
	cd "${srcdir}/${pkgname}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# -*- mode: bash;-*-

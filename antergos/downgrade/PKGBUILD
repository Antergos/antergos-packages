# Author: Patrick Brisbin <pbrisbin@gmail.com>
# Maintainer: Antergos Devs <devs@antergos.com>

pkgname=downgrade
pkgver=6.2.1
pkgrel=2
pkgdesc="Bash script for downgrading one or more packages to a version in your cache or the A.L.A."
arch=('any')
url="https://github.com/pbrisbin/$pkgname"
license=('GPL')
source=("https://github.com/pbrisbin/$pkgname/archive/v$pkgver.tar.gz")
depends=('pacman-contrib')
optdepends=('sudo: for installation via sudo')
md5sums=('5fd0d700057191947fb745ead45ec24c')

package() {
  local po_file

  cd "$pkgname-$pkgver"

  for po_file in locale/*.po; do
    locale="$(basename "$po_file" .po)"

    mkdir -p "$pkgdir/usr/share/locale/$locale/LC_MESSAGES/"
    msgfmt "$po_file" -o "$pkgdir/usr/share/locale/$locale/LC_MESSAGES/$pkgname.mo"
  done

  make PREFIX=/usr DESTDIR="$pkgdir" install
}

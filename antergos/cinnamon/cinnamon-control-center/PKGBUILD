# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-control-center
pkgver=3.6.0
pkgrel=1
pkgdesc='Modules for Cinnamon Settings'
url='https://github.com/linuxmint/cinnamon-control-center'
arch=('i686' 'x86_64')
groups=('cinnamon')
depends=('cinnamon-settings-daemon' 'cinnamon-translations' 'cinnamon-menus'
		 'libgnomekbd' 'network-manager-applet')
optdepends=('gnome-color-manager: for color management tasks')
makedepends=('intltool' 'gnome-common' 'docbook-xsl')
license=('GPL')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-control-center/archive/$pkgver.tar.gz")
md5sums=('b7750bd99216b00a151193192ec0ec23')


# BEGIN ANTBS METADATA

_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='linuxmint'
_monitored_repo='cinnamon-control-center'

# END ANTBS METADATA


build() {
	cd "${pkgname}-${pkgver}"

	./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static \
		--enable-systemd \
		--disable-update-mimedb

	make
}

package() {
	cd "${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	rm "${pkgdir}"/usr/share/applications/cinnamon-*-panel.desktop
	# Fix a warning
	chown 102:0 "$pkgdir/usr/share/polkit-1/rules.d"
	chmod 750 "$pkgdir/usr/share/polkit-1/rules.d"
}

# -*- mode: bash;-*-

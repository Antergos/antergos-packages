# $Id: PKGBUILD 126461 2015-01-23 20:22:27Z faidoc $
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ner0

pkgname=nemo
pkgver=3.0.6
pkgrel=2
pkgdesc="File manager for GTK-based desktops."
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
license=('GPL')
groups=('cinnamon')
depends=('libexif' 'gvfs' 'dconf' 'desktop-file-utils' 'exempi' 'python2'
		'cinnamon-desktop' 'libnotify' 'libxml2' 'cinnamon-translations')
makedepends=('gtk-doc' 'gobject-introspection' 'intltool' 'gnome-common'
			'python-gobject' 'python-polib' 'python2-gobject' 'python2-polib')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/nemo/tarball/$pkgver"
		'fix-log-spam.patch::https://patch-diff.githubusercontent.com/raw/linuxmint/nemo/pull/1209.patch')
sha256sums=('77d5c47a6657dfbaf66c9f80978ce8650595753138d27bd2adaebbe0e46c3e23')


# BEGIN ANTBS METADATA

_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='linuxmint'
_monitored_repo='nemo'

# END ANTBS METADATA


prepare() {
	cd linuxmint-nemo-*

	# Python2 fix
	find -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

	# Rename 'Files' app name to avoid having the same as nautilus
	sed -i 's/^Name\(.*\)=.*/Name\1=Nemo/' data/nemo.desktop.in.in

	# https://github.com/linuxmint/nemo/pull/1209
	[[ '3.0.6' = "${pkgver}" && '2' = "${pkgrel}" ]] && patch -Np1 -i ../fix-log-spam.patch

}


build() {
	cd linuxmint-nemo-*

	./autogen.sh --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var --disable-static \
		--libexecdir=/usr/lib/nemo \
		--disable-update-mimedb \
		--disable-tracker \
		--disable-gtk-doc-html \
		--disable-schemas-compile
	make
}

package() {
	cd linuxmint-nemo-*

	make DESTDIR="${pkgdir}" install
}

# -*- mode: bash;-*-
 

# Maintainer: Antergos Developers <dev@antergos.com>

pkgname='qt5-styleplugins'
_srcname='qtstyleplugins'
pkgver=5.0.0.1r33
pkgrel=1
pkgdesc='Additional style plugins for Qt5'
arch=('i686' 'x86_64')
url="https://github.com/qtproject/${_srcname}"
license=('LGPL')
depends=('qt5-base' 'gconf' 'gtk2' 'libx11')
makedepends=('git')
conflicts=("${pkgname}-git")
_commit='84b443109729664c7a0bd124b42c493f28069efc'
source=("${_srcname}-${_commit}.zip::${url}/archive/${_commit}.zip")
md5sums=('d54592bc206057af62459199a5bac3e7')


build() {
	cd "${srcdir}/${_srcname}-${_commit}"

	qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib

	make
}

package() {
	cd "${srcdir}/${_srcname}-${_commit}"

	make INSTALL_ROOT="${pkgdir}" install
	rm --recursive "${pkgdir}/usr/lib/cmake"
}

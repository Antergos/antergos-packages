# Maintainer: Antergos Developers <dustin@antergos.com>
# Contributor: Gifts <gifts.antichat@gmail.com>
# Contributor: Andrey Vlasovskikh <andrey.vlasovskikh@gmail.com>

pkgname=intellij-idea-ue-eap
_buildver=145.971
_pkgver=2016.2
_eap="False"
pkgver="${_pkgver}.${_buildver}"
pkgrel=1
pkgdesc="Intelligent Java IDE with out-of-the-box support for web, enterprise, and mobile frameworks. Early Access Program (EAP) Build."
arch=('any')
options=('!strip')
url="http://www.jetbrains.com/idea/"
license=("custom")
depends=('giflib' 'libxtst')
makedepends=('wget')
conflicts=("intellij-idea-ultimate-edition")
groups=("development" "IDE" "editor" "jetbrains")


if [[ "True" = "${_eap}" ]]; then
	_srcfile="ideaIU-${_buildver}.tar.gz"
	#_srcfile="ideaIU-16-Preview.tar.gz"
	source=("http://download.jetbrains.com/idea/${_srcfile}"
			"${pkgname}.desktop")
	sha256sums=($(wget -q "${source}.sha256" && cat "${_srcfile}.sha256" | cut -f1 -d" ")
				"c6ffb4c3a14b4b6a90158fda92aab7dbee2bb32d9585b801682792966699919c")
else
	_srcfile="ideaIU-${_pkgver}.tar.gz"
	source=("http://download.jetbrains.com/idea/${_srcfile}"
			"${pkgname}.desktop")
	sha256sums=($(wget -q "${source}.sha256" && cat "${_srcfile}.sha256" | cut -f1 -d" ")
				"c6ffb4c3a14b4b6a90158fda92aab7dbee2bb32d9585b801682792966699919c")
fi

package() {
	cd "${srcdir}"
	install -dm 755 \
		"${pkgdir}/opt/${pkgname}" \
		"${pkgdir}/usr/bin/" \
		"${pkgdir}/usr/share/licenses/${pkgname}/" \
		"${pkgdir}/usr/share/applications/"

	if [[ "True" = "${_eap}" ]]; then
		cp -R --no-preserve=ownership "${srcdir}/idea-IU-${_buildver}/"* "${pkgdir}/opt/${pkgname}"
	else
		cp -R --no-preserve=ownership "${srcdir}/idea-IU-${_pkgver}/"* "${pkgdir}/opt/${pkgname}"
	fi
	
	if [[ "i686" = "${CARCH}" ]]; then
		rm -f "${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux64.so"
		rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier64"
	fi

	sed -i "s/Version=/Version=${pkgver}/g" "${pkgname}.desktop"
	install -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/"
	
	ln -s "/opt/${pkgname}/bin/idea.sh" "${pkgdir}/usr/bin/idea-ue-eap"
}

# -*- mode: bash;-*-

# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Christian Hesse <mail@eworm.de>

pkgname=opera-developer
pkgver=32.0.1933.0
pkgrel=1
pkgdesc='A fast and secure web browser and Internet suite - developer stream'
arch=('i686' 'x86_64')
url='http://www.opera.com/browser/'
license=('custom:opera')
depends=('alsa-lib' 'nss' 'gtk2' 'gconf' 'libxss' 'libxtst' 'desktop-file-utils')
optdepends=(
	'ffmpeg: HTML5 H264 and mp3 playback'
	'curl: opera crash reporter and autoupdate checker'
)
install=opera.install
options=(!strip)
source_i686=("http://get.geo.opera.com/pub/${pkgname}/${pkgver}/linux/${pkgname}_${pkgver}_i386.deb")
source_x86_64=("http://get.geo.opera.com/pub/${pkgname}/${pkgver}/linux/${pkgname}_${pkgver}_amd64.deb")
sha256sums_i686=('be60c87212307a06d7628761b9bb16e15c2bc7d711957467b4c55e2f7f868b32')
sha256sums_x86_64=('36e153c3778ab543ea0b0921534f42dd689aaadd1fe0aac43d5e5fcb832a8a58')

#prepare() {
#}

package() {
	tar -xf data.tar.xz --exclude=usr/share/{lintian,menu} -C "$pkgdir/"
	
	# license
	install -Dm644 \
		"$pkgdir/usr/share/doc/$pkgname/copyright" \
		"$pkgdir/usr/share/licenses/$pkgname/copyright"
	
	cd ${pkgdir}/usr/lib/*-linux-gnu
	cp -R ${pkgname} ${pkgdir}/usr/lib
	
	# set suid bit for Opera sandbox
	chmod 4755 "${pkgdir}/usr/lib/${pkgname}/opera_sandbox"
	
	unlink ${pkgdir}/usr/bin/${pkgname}
	cd ${pkgdir}/usr/bin
	ln -sf /usr/lib/${pkgname}/${pkgname}
}

